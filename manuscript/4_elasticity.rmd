```{r setup, include = FALSE, echo = FALSE}
library(here)
knitr::opts_chunk$set(
  message = FALSE, 
  warning = FALSE, 
  echo = FALSE, 
  cache = FALSE,
  include = FALSE,
  fig.width = 10, fig.height = 6,
  fig.align = "center"
)
knitr::opts_knit$set(root.dir = here())

library(tidyverse)
library(readstata13)
library(kableExtra)
options(knitr.kable.NA = "")
```

```{stata dataset, collectcode=TRUE, results = "hide", eval = FALSE}
cd "C:\Users\vge00\Desktop\nastab"  //root path

use "data\shaped.dta", clear
tsset pid year

gen log_price = ln(price)
gen log_lprice = ln(lprice)
gen log_iv1price = ln(iv1price)
gen log_iv2price = ln(iv2price)
gen log_iv3price = ln(iv3price)
gen log_total_g = ln(i_total_giving + 1)
gen log_pinc_all = ln(lincome + 100000)

forvalues i = 1(1)3 {
	gen lag`i'_log_total_g = l`i'.log_total_g
	gen lag`i'_log_pinc_all = l`i'.log_pinc_all
	gen lag`i'_age = l`i'.age
	gen lag`i'_sqage = l`i'.sqage
	
	gen log_diff`i'g = log_total_g - lag`i'_log_total_g
	gen log_diff`i'I = log_pinc_all - lag`i'_log_pinc_all
	gen diff`i'_age = age - lag`i'_age
	gen diff`i'_sqage = sqage - lag`i'_sqage
}

keep if year >= 2012 & age >= 24

** ---- merged with balancedt
merge m:1 pid using "data\shape\balanceid.dta"
drop _merge

foreach v in log_price log_lprice log_iv1price log_iv2price log_iv3price {
    gen `v'_int2 = `v' * (balance3 == 2) if !missing(balance3)
	gen `v'_int3 = `v' * (balance3 == 3) if !missing(balance3)
}
```

# Main Results

## Price and Income Elasticity

```{stata Elasticity, results = "hide", eval = FALSE}
xtreg log_total_g log_price log_pinc_all i.year, fe vce(cluster pid)

mat coeftab = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab = Logprice_M0 Loginc_M0

mat stattab = e(N) \ e(r2)
mat colnames stattab = M0
mat rownames stattab = N r2

predict resid, e

* with covariates
local cov sqage i.year##i.educ i.year##i.gender i.year##i.living_area
local xvars 
local k = 1
foreach v of local cov {
	di "model `k'"
    
	*make set of covariates
	local xvars `xvars' `v'
	
	*estimate fixed effect model
	xtreg log_total_g log_price log_pinc_all i.year age `xvars', fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price".."log_pinc_all"]
	mat colnames coef = Logprice_M`k' Loginc_M`k'
	
	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r2
	
	*combined with previous results
	mat_capp coeftab : coeftab coef
	mat_capp stattab : stattab stat

	local k = `++k'
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/ElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/ElasticityStat.dta, replace) rownames(xvar) names(col)

* residuals plot
frame copy default plotdt
frame plotdt {
	by year benefit_group, sort: egen meang = mean(resid)
	
	keep meang year benefit_group
	duplicates drop
	keep if !missing(benefit_group)
	
	gen meang_norm = meang if year == 2013
	by benefit_group, sort: egen meang13 = mean(meang_norm) 
	gen meang_n = meang - meang13
}

frame plotdt {
	twoway ///
	(connected meang_n year if benefit_group == 1, sort msymbol(O) color(black))  ///
	(connected meang_n year if benefit_group == 2, sort msymbol(T) color(black))  ///
	(connected meang_n year if benefit_group == 3, sort msymbol(S) color(black)),  ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	ytitle("Average Residuals (Normalized)")  ///
	legend(label(1 "Income {&lt} 1200") label(2 " Income in [1200, 4600)") label(3 "Income {&ge} 4600"))  ///
	graphregion(fcolor(white))

	graph export "_assets/ElasticityResid.pdf", replace
}

frame drop plotdt
```

```{stata ExtElasticity, results = "hide", eval = FALSE}
* baseline
xtreg i_ext_giving log_price log_pinc_all i.year, fe vce(cluster pid)

mat coeftab = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab = Logprice_M0 Loginc_M0

mat stattab = e(N) \ e(r2)
mat colnames stattab = M0
mat rownames stattab = N r

predict residext, e

* price elasticity evaluated at mean
summarize i_ext_giving
local mu = r(mean)
lincom log_price*(1/`mu')

mat elas1 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas1 = Logprice_M0
mat rownames elas1 = e_b e_se e_pval

* price elasticity evaluated at mean
summarize i_ext_giving
local mu = r(mean)
lincom log_pinc_all*(1/`mu')

mat elas2 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas2 = Loginc_M0
mat rownames elas2 = e_b e_se e_pval

* baseline result
mat_capp elas : elas1 elas2
mat_rapp coeftab : coeftab elas


* with covariates
local cov sqage i.year##i.educ i.year##i.gender i.year##i.living_area
local xvars 
local k = 1
foreach v of local cov {
	di "model `k'"
    
	*make set of covariates
	local xvars `xvars' `v'
	
	*estimate fixed effect model
	xtreg i_ext_giving log_price log_pinc_all i.year age `xvars', fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
	mat colnames coef = Logprice_M`k' Loginc_M`k'

	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r
	
	*proportion of donors
	summarize i_ext_giving
	local mu = r(mean)
	
	* price elasticity evaluated at mean
	summarize i_ext_giving
	local mu = r(mean)
	lincom log_price*(1/`mu')

	mat elas1 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
	mat colnames elas1 = Logprice_M`k'
	mat rownames elas1 = e_b e_se e_pval

	* price elasticity evaluated at mean
	summarize i_ext_giving
	local mu = r(mean)
	lincom log_pinc_all*(1/`mu')

	mat elas2 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
	mat colnames elas2 = Loginc_M`k'
	mat rownames elas2 = e_b e_se e_pval
	
	*combined with previous results
	mat_capp elas : elas1 elas2
	mat_rapp coef : coef elas
	mat_capp coeftab : coeftab coef
	
	mat_capp stattab : stattab stat

	local k = `++k'
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/ExtElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/ExtElasticityStat.dta, replace) rownames(xvar) names(col)

* residuals plot
frame copy default plotdt
frame plotdt {
	by year benefit_group, sort: egen meang = mean(residext)
	
	keep meang year benefit_group
	duplicates drop
	keep if !missing(benefit_group)
	
	gen meang_norm = meang if year == 2013
	by benefit_group, sort: egen meang13 = mean(meang_norm) 
	gen meang_n = meang - meang13
}

frame plotdt {
	twoway ///
	(connected meang_n year if benefit_group == 1, sort msymbol(O) color(black))  ///
	(connected meang_n year if benefit_group == 2, sort msymbol(T) color(black))  ///
	(connected meang_n year if benefit_group == 3, sort msymbol(S) color(black)),  ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	ytitle("Average Residuals (Normalized)")  ///
	legend(label(1 "Income {&lt} 1200") label(2 " Income in [1200, 4600)") label(3 "Income {&ge} 4600"))  ///
	graphregion(fcolor(white))

	graph export "_assets/ExtElasticityResid.pdf", replace
}

frame drop plotdt
```

```{stata IntElasticity, results = "hide", eval = FALSE}
* baseline
xtreg log_total_g log_price log_pinc_all i.year if i_ext_giving == 1, fe vce(cluster pid)

mat coeftab = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab = Logprice_M0 Loginc_M0

mat stattab = e(N) \ e(r2)
mat colnames stattab = M0
mat rownames stattab = N r2

predict residint, e

* with covariates
local cov sqage i.year##i.educ i.year##i.gender i.year##i.living_area
local xvars 
local k = 1
foreach v of local cov {
	di "model `k'"
    
	*make set of covariates
	local xvars `xvars' `v'
	
	*estimate fixed effect model
	xtreg log_total_g log_price log_pinc_all i.year age `xvars' if i_ext_giving == 1, fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price".."log_pinc_all"]
	mat colnames coef = Logprice_M`k' Loginc_M`k'
	
	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r2
	
	*combined with previous results
	mat_capp coeftab : coeftab coef
	mat_capp stattab : stattab stat

	local k = `++k'
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/IntElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/IntElasticityStat.dta, replace) rownames(xvar) names(col)

* residuals plot
frame copy default plotdt
frame plotdt {
	by year benefit_group, sort: egen meang = mean(residint)
	
	keep meang year benefit_group
	duplicates drop
	keep if !missing(benefit_group)
	
	gen meang_norm = meang if year == 2013
	by benefit_group, sort: egen meang13 = mean(meang_norm) 
	gen meang_n = meang - meang13
}

frame plotdt {
	twoway ///
	(connected meang_n year if benefit_group == 1, sort msymbol(O) color(black))  ///
	(connected meang_n year if benefit_group == 2, sort msymbol(T) color(black))  ///
	(connected meang_n year if benefit_group == 3, sort msymbol(S) color(black)),  ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	ytitle("Average Residuals (Normalized)")  ///
	legend(label(1 "Income {&lt} 1200") label(2 " Income in [1200, 4600)") label(3 "Income {&ge} 4600"))  ///
	graphregion(fcolor(white))

	graph export "_assets/IntElasticityResid.pdf", replace
}

frame drop plotdt
```


```{r shapeEstimateElasticity, eval = FALSE}
orgtab <- list(
	overall = read.dta13("_assets/EstimateElasticity.dta") %>% data.frame(),
	intensive = read.dta13("_assets/EstimateElasticityIntensive.dta") %>% data.frame(),
	extensive = read.dta13("_assets/EstimateElasticityExtensive.dta") %>% data.frame()
)

coeftab <- orgtab %>% 
	purrr::map(function(x)
		x %>% 
		mutate(
			b = case_when(
				pvalue <= 0.01 ~ sprintf("%1.3f***", b),
				pvalue <= 0.05 ~ sprintf("%1.3f**", b),
				pvalue <= 0.1 ~ sprintf("%1.3f*", b),
				TRUE ~ sprintf("%1.3f", b)
			),
			se = sprintf("(%1.3f)", se),
			N = sprintf("%1d", N)
		) %>% 
		select(xvar, b, se, N) %>% 
		pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
		pivot_wider(values_from = "value", names_from = "xvar") %>% 
		select(-stat) %>% 
		cbind(xvar = c("ln(giving price)", "", "N"), .)
	) %>% 
	reduce(rbind)

elastab <- orgtab$extensive %>% 
	mutate(
		e_b = case_when(
			e_pval <= 0.01 ~ sprintf("%1.3f***", e_b),
			e_pval <= 0.05 ~ sprintf("%1.3f**", e_b),
			e_pval <= 0.1 ~ sprintf("%1.3f*", e_b),
			TRUE ~ sprintf("%1.3f", e_b)
		),
		e_se = sprintf("(%1.3f)", e_se)
	) %>% 
	select(xvar, e_b, e_se) %>% 
	pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
	pivot_wider(values_from = "value", names_from = "xvar") %>% 
	select(-stat) %>% 
	cbind(xvar = c("Price elasticity at mean", ""), .)

result.tab <- rbind(coeftab[-9,], elastab) %>% rbind(coeftab[9,])

addline <- cbind(
	xvar = c("Time FE", "Age", "Year X Education", "Year X Gender", "Resident Area"),
	model0 = c("Y", rep("N", 4)),
	model1 = c(rep("Y", 2), rep("N", 3)),
	model2 = c(rep("Y", 3), rep("N", 2)),
	model3 = c(rep("Y", 4), rep("N", 1)),
	model4 = rep("Y", 5)
)

tab1 <- rbind(result.tab[1:2,], addline) %>% rbind(result.tab[3,])
tab2 <- rbind(result.tab[4:10,], addline) %>% rbind(result.tab[11,])
```

<!--- Slide
## Baseline Regressions: Result
We found the **price effect** of giving (1\% price increase leads to about 1.1\% giving decrease)
-->

```{r kableEstimateElasticityPart1, include = TRUE, eval = FALSE}
knitr::kable(
    tab1,
    format = "latex",
    caption = "Main Results",
    col.names = c("", "(1)", "(2)", "(3)", "(4)", "(5)"),
    row.names = FALSE,
    align = "lccccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() %>% 
footnote(general_title = "", general = "Notes: $^{*}$ $p < 0.1$, $^{**}$ $p < 0.05$, $^{***}$ $p < 0.01$. Standard errors are clustered at individual level. When controlling age, we alson include its squared term.", threeparttable = TRUE, escape = FALSE)
```


<!--- Slide
## Intensive Margin and Extensive Margin: Result
--->

```{r kableEstimateElasticityPart2, include = TRUE, eval = FALSE}
knitr::kable(
    tab2,
    format = "latex",
    caption = "Main Results: Intensive- and Extensive-Margin Elasticity",
    col.names = c("", "(1)", "(2)", "(3)", "(4)", "(5)"),
    row.names = FALSE,
    align = "lccccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() %>% 
pack_rows("Intensive-Margin Elasticity", 1, 3) %>% 
pack_rows("Extensive-Margin Elasticity", 4, 13) %>% 
footnote(general_title = "", general = "Notes: $^{*}$ $p < 0.1$, $^{**}$ $p < 0.05$, $^{***}$ $p < 0.01$. Standard errors are clustered at individual level. When controlling age, we alson include its squared term. The implied extensive-marign price elasticity is evaluated at the sample mean of $D_{ijt}$.", threeparttable = TRUE, escape = FALSE)
```


## Robustness Check

<!--- Slide
1. Income and donations are determined simultaneously
1. Effect of presidential transition on donation behavior
1. Last price elasticity
1. Self-selection of receiving tax benefit
1. Transitory and permanent elasticity
--->

<!--- Slide
## Robustness Check 1

First potential concern: Income and donations are determined simultaneously
    
- This causes both a change of giving price and a change of an amount of donations
- Gruber and Saez (2002) provided that we should use $\log(\text{Price}_{ijt}/\text{Price}_{ij(t-k)})$ as an insturment.
- We estimated the model (5) in the previous slide, using the panel IV model for $k = 1, 2, 3$.
	- Note that Alumnia (2020) took a strategy of $k$-difference model.
--->

```{stata LastElasticity, results = "hide", eval = FALSE}
* first stage
xtreg log_lprice log_price log_pinc_all i.year, fe vce(cluster pid)

mat fstage = r(table)["b".."pvalue","log_price"]
mat fstage = fstage[1,1] \ fstage[3,1]^2
mat colnames fstage = M0
mat rownames fstage = ivcoef ivf

* second stage
xtivreg log_total_g log_pinc_all i.year (log_lprice = log_price), fe vce(cluster pid)

mat coeftab = r(table)["b".."pvalue", "log_lprice".."log_pinc_all"]
mat colnames coeftab = Loglprice_M0 Loginc_M0

mat stattab = e(N) \ e(r2)
mat colnames stattab = M0
mat rownames stattab = N r2

mat_rapp stattab : stattab fstage

* with covariates
local cov sqage i.year##i.educ i.year##i.gender i.year##i.living_area
local xvars 
local k = 1
foreach v of local cov {
	di "model `k'"
    
	*make set of covariates
	local xvars `xvars' `v'
	
	*first stage
	xtreg log_lprice log_price log_pinc_all i.year age `xvars', fe vce(cluster pid)
	
	mat fstage = r(table)["b".."pvalue","log_price"]
	mat fstage = fstage[1,1] \ fstage[3,1]^2
	mat colnames fstage = M`k'
	mat rownames fstage = ivcoef ivf
	
	*second stage
	xtivreg log_total_g log_pinc_all i.year age `xvars' (log_lprice = log_price), fe vce(cluster pid)
	
	mat coef = r(table)["b".."pvalue", "log_lprice".."log_pinc_all"]
	mat colnames coef = Loglprice_M`k' Loginc_M`k'

	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r2
	
	mat_rapp stat : stat fstage
	
	*combined with previous results
	mat_capp coeftab : coeftab coef
	mat_capp stattab : stattab stat

	local k = `++k'
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/LastElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/LastElasticityStat.dta, replace) rownames(xvar) names(col)
```

```{stata LastExtElasticity, results = "hide", eval = FALSE}
* first stage
xtreg log_lprice log_price log_pinc_all i.year, fe vce(cluster pid)

mat fstage = r(table)["b".."pvalue","log_price"]
mat fstage = fstage[1,1] \ fstage[3,1]^2
mat colnames fstage = M0
mat rownames fstage = ivcoef ivf

* second stage
xtivreg i_ext_giving log_pinc_all i.year (log_lprice = log_price), fe vce(cluster pid)

mat coeftab = r(table)["b".."pvalue", "log_lprice".."log_pinc_all"]
mat colnames coeftab = Loglprice_M0 Loginc_M0

mat stattab = e(N) \ e(r2)
mat colnames stattab = M0
mat rownames stattab = N r2

* price elasticity evaluated at mean
summarize i_ext_giving
local mu = r(mean)
lincom log_lprice*(1/`mu')

mat elas1 = r(estimate) \ r(se) \ (1 - normal(abs(r(estimate)/r(se))))*2
mat colnames elas1 = Loglprice_M0
mat rownames elas1 = e_b e_se e_pval

* price elasticity evaluated at mean
summarize i_ext_giving
local mu = r(mean)
lincom log_pinc_all*(1/`mu')

mat elas2 = r(estimate) \ r(se) \ (1 - normal(abs(r(estimate)/r(se))))*2
mat colnames elas2 = Loginc_M0
mat rownames elas2 = e_b e_se e_pval

mat_capp elas : elas1 elas2
mat_rapp coeftab : coeftab elas
mat_rapp stattab : stattab fstage

* with covariates
local cov sqage i.year##i.educ i.year##i.gender i.year##i.living_area
local xvars 
local k = 1
foreach v of local cov {
	di "model `k'"
    
	*make set of covariates
	local xvars `xvars' `v'
	
	*first stage
	xtreg log_lprice log_price log_pinc_all i.year age `xvars', fe vce(cluster pid)
	
	mat fstage = r(table)["b".."pvalue","log_price"]
	mat fstage = fstage[1,1] \ fstage[3,1]^2
	mat colnames fstage = M`k'
	mat rownames fstage = ivcoef ivf
	
	*second stage
	xtivreg i_ext_giving log_pinc_all i.year age `xvars' (log_lprice = log_price), fe vce(cluster pid)
	
	mat coef = r(table)["b".."pvalue", "log_lprice".."log_pinc_all"]
	mat colnames coef = Loglprice_M`k' Loginc_M`k'

	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r2
	
	* price elasticity evaluated at mean
	summarize i_ext_giving
	local mu = r(mean)
	lincom log_lprice*(1/`mu')

	mat elas1 = r(estimate) \ r(se) \ (1 - normal(abs(r(estimate)/r(se))))*2
	mat colnames elas1 = Loglprice_M`k'
	mat rownames elas1 = e_b e_se e_pval

	* price elasticity evaluated at mean
	summarize i_ext_giving
	local mu = r(mean)
	lincom log_pinc_all*(1/`mu')

	mat elas2 = r(estimate) \ r(se) \ (1 - normal(abs(r(estimate)/r(se))))*2
	mat colnames elas2 = Loginc_M`k'
	mat rownames elas2 = e_b e_se e_pval

	mat_capp elas : elas1 elas2
	mat_rapp coef : coef elas
	mat_rapp stat : stat fstage
	
	*combined with previous results
	mat_capp coeftab : coeftab coef
	mat_capp stattab : stattab stat

	local k = `++k'
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/LastExtElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/LastExtElasticityStat.dta, replace) rownames(xvar) names(col)
```

```{stata LastIntElasticity, results = "hide", eval = FALSE}
* first stage
xtreg log_lprice log_price log_pinc_all i.year if i_ext_giving == 1, fe vce(cluster pid)

mat fstage = r(table)["b".."pvalue","log_price"]
mat fstage = fstage[1,1] \ fstage[3,1]^2
mat colnames fstage = M0
mat rownames fstage = ivcoef ivf

* second stage
xtivreg log_total_g log_pinc_all i.year (log_lprice = log_price) if i_ext_giving == 1, fe vce(cluster pid)

mat coeftab = r(table)["b".."pvalue", "log_lprice".."log_pinc_all"]
mat colnames coeftab = Loglprice_M0 Loginc_M0

mat stattab = e(N) \ e(r2)
mat colnames stattab = M0
mat rownames stattab = N r2

mat_rapp stattab : stattab fstage

* with covariates
local cov sqage i.year##i.educ i.year##i.gender i.year##i.living_area
local xvars 
local k = 1
foreach v of local cov {
	di "model `k'"
    
	*make set of covariates
	local xvars `xvars' `v'
	
	*first stage
	xtreg log_lprice log_price log_pinc_all i.year age `xvars' if i_ext_giving == 1, fe vce(cluster pid)
	
	mat fstage = r(table)["b".."pvalue","log_price"]
	mat fstage = fstage[1,1] \ fstage[3,1]^2
	mat colnames fstage = M`k'
	mat rownames fstage = ivcoef ivf
	
	*second stage
	xtivreg log_total_g log_pinc_all i.year age `xvars' (log_lprice = log_price) if i_ext_giving == 1, fe vce(cluster pid)
	
	mat coef = r(table)["b".."pvalue", "log_lprice".."log_pinc_all"]
	mat colnames coef = Loglprice_M`k' Loginc_M`k'

	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r2
	
	mat_rapp stat : stat fstage
	
	*combined with previous results
	mat_capp coeftab : coeftab coef
	mat_capp stattab : stattab stat

	local k = `++k'
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/LastIntElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/LastIntElasticityStat.dta, replace) rownames(xvar) names(col)
```

<!--- Slide
## Robustness Check 1: Result
---> 

```{r shapePanelIVEstimateElasticity, eval = FALSE}
org.pivreg <- read.dta13("_assets/PanelIVEstimateElasticity.dta") %>% data.frame()

coef.pivreg <- org.pivreg %>% 
	select(xvar, b, se, pvalue) %>% 
	mutate(
		b = case_when(
			pvalue <= 0.01 ~ sprintf("%1.3f***", b),
			pvalue <= 0.05 ~ sprintf("%1.3f**", b),
			pvalue <= 0.1 ~ sprintf("%1.3f*", b),
			TRUE ~ sprintf("%1.3f", b)
		),
		se = sprintf("(%1.3f)", se)
	) %>% 
	select(xvar, b, se) %>% 
	pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
	pivot_wider(values_from = "value", names_from = "xvar") %>% 
	select(-stat) %>% 
	cbind(xvar = c("ln(giving price)", ""), .)

stat.pivreg <- org.pivreg %>% 
	select(xvar, ivf, N) %>% 
	mutate(ivf = sprintf("%1.2f", ivf), N = sprintf("%1d", as.integer(N))) %>%
	pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
	pivot_wider(values_from = "value", names_from = "xvar") %>% 
	select(-stat) %>% 
	cbind(xvar = c("F-stat of IV", "N"), .)

addline <- cbind(
	xvar = c("Individual FE", "Time FE", "Other Controls"),
	model1 = rep("Y", 3),
	model2 = rep("Y", 3),
	model3 = rep("Y", 3)
)

tab.pivreg <- rbind(coef.pivreg, addline) %>% rbind(stat.pivreg)
```

```{r kablePanelIVEstimateElasticity, include = TRUE, eval = FALSE}
knitr::kable(
    tab.pivreg,
    format = "latex",
    caption = "Panel IV Results",
    col.names = c("", "(1)", "(2)", "(3)"),
    row.names = FALSE,
    align = "lccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() %>% 
add_header_above(c("Lag $k$" = 1, "$k = 1$" = 1, "$k = 2$" = 1, "$k = 3$" = 1), escape = FALSE) %>%
footnote(general_title = "", general = "Notes: $^{*}$ $p < 0.1$, $^{**}$ $p < 0.05$, $^{***}$ $p < 0.01$. Standard errors are clustered at individual level. Other controls are age (its squared value), the interaction between year dummies and education dummies, the interaction between year dummies and gender dummies, and resident area. The instumental variable is $\\\\log(\\\\text{Price}_{ijt}/\\\\text{Price}_{ij(t-k)})$.", threeparttable = TRUE, escape = FALSE)
```

<!--- Slide
## Robustness Check 1: Intensive and Extensive Margin
--->

```{r shapePanelIVEstimateElasticityIntExt, eval = FALSE}
org.pivreg <- list(
	intensive = read.dta13("_assets/PanelIVEstimateElasticityIntensive.dta") %>% data.frame(),
	extensive = read.dta13("_assets/PanelIVEstimateElasticityExtensive.dta") %>% data.frame()
)

coef.pivreg <- org.pivreg %>% 
	purrr::map(function(x)
		x %>% 
		select(xvar, b, se, pvalue, ivf, N) %>% 
		mutate(
			b = case_when(
				pvalue <= 0.01 ~ sprintf("%1.4f***", b),
				pvalue <= 0.05 ~ sprintf("%1.4f**", b),
				pvalue <= 0.1 ~ sprintf("%1.4f*", b),
				TRUE ~ sprintf("%1.4f", b)
			),
			se = sprintf("(%1.4f)", se),
			ivf = sprintf("%1.2f", ivf), N = sprintf("%1d", as.integer(N))
		) %>% 
		select(xvar, b, se, ivf, N) %>% 
		pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
		pivot_wider(values_from = "value", names_from = "xvar") %>% 
		select(-stat) %>% 
		cbind(xvar = c("ln(giving price)", "", "F-stat of IV", "N"), .)
	) %>% 
	reduce(rbind)

elas.pivreg <- org.pivreg$extensive %>% 
	mutate(
		e_b = case_when(
			e_pval <= 0.01 ~ sprintf("%1.3f***", e_b),
			e_pval <= 0.05 ~ sprintf("%1.3f**", e_b),
			e_pval <= 0.1 ~ sprintf("%1.3f*", e_b),
			TRUE ~ sprintf("%1.3f", e_b)
		),
		e_se = sprintf("(%1.3f)", e_se)
	) %>% 
	select(xvar, e_b, e_se) %>% 
	pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
	pivot_wider(values_from = "value", names_from = "xvar") %>% 
	select(-stat) %>% 
	cbind(xvar = c("Price elasticity at mean", ""), .)

addline <- cbind(
	xvar = c("Individual FE", "Time FE", "Other Controls"),
	model1 = rep("Y", 3),
	model2 = rep("Y", 3),
	model3 = rep("Y", 3)
)

tab.pivreg <- rbind(coef.pivreg[1:6,], elas.pivreg) %>% 
	rbind(addline) %>% 
	rbind(coef.pivreg[7:8,])
```

```{r kablePanelIVEstimateElasticityIntExt, include = TRUE, eval = FALSE}
knitr::kable(
    tab.pivreg,
    format = "latex",
    caption = "Panel IV Results: Intensive- and Extensive-Margin Elasticity",
    col.names = c("", "(1)", "(2)", "(3)"),
    row.names = FALSE,
    align = "lccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() %>% 
add_header_above(c("Lag $k$" = 1, "$k = 1$" = 1, "$k = 2$" = 1, "$k = 3$" = 1), escape = FALSE) %>%
pack_rows("Intensive Margin", 1, 4) %>% 
pack_rows("Extensive Margin", 5, 13) %>% 
footnote(general_title = "", general = "Notes: $^{*}$ $p < 0.1$, $^{**}$ $p < 0.05$, $^{***}$ $p < 0.01$. Standard errors are clustered at individual level. Other controls are age (its squared value), the interaction between year dummies and education dummies, the interaction between year dummies and gender dummies, and resident area. The instumental variable is $\\\\log(\\\\text{Price}_{ijt}/\\\\text{Price}_{ij(t-k)})$. The implied extensive-marign price elasticity is evaluated at the sample mean of $D_{ijt}$.", threeparttable = TRUE, escape = FALSE)
```

<!--- Slide
## Robust Check 2

Second potential concern: The effect of presidential transition on donations 
    
- The presidential transition is one of our major ommited factor to charitable giving.
	- In May 2017, South Korea president changed from Park Geun-hye to Moon Jae-in. This presidential transition was due to the impeachment charge against Park Geun-hye. People became distrustful of her due to the shinking of MV Sewol (April 2014).
- To shed light on this concern, we used data in 2013 and 2014 (President was Park Geun-hye in both years), and estimated the model (5) in the previous slide, using the fixed effect model and the panel IV model for $k = 1, 2, 3$.
--->


```{stata ShortElasticity, results = "hide", eval = FALSE}
* M0: year >= 2013 without cov
xtreg log_total_g log_price log_pinc_all i.year if year >= 2013, fe vce(cluster pid)

mat coeftab0 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab0 = Logprice_M0 Loginc_M0

mat stattab0 = e(N) \ e(r2)
mat colnames stattab0 = M0
mat rownames stattab0 = N r2

* M1: year >= 2013 with cov
xtreg log_total_g log_price log_pinc_all i.year age sqage i.year##i.educ i.year##i.gender i.year##i.living_area ///
	if year >= 2013, fe vce(cluster pid)

mat coeftab1 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab1 = Logprice_M1 Loginc_M1

mat stattab1 = e(N) \ e(r2)
mat colnames stattab1 = M1
mat rownames stattab1 = N r2

* M2: year == 2013 | 2014 without cov
xtreg log_total_g log_price log_pinc_all i.year if year == 2013 | year == 2014, fe vce(cluster pid)

mat coeftab2 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab2 = Logprice_M2 Loginc_M2

mat stattab2 = e(N) \ e(r2)
mat colnames stattab2 = M2
mat rownames stattab2 = N r2

* M3: year == 2013 | 2014 with cov
xtreg log_total_g log_price log_pinc_all i.year age sqage i.year##i.educ i.year##i.gender i.year##i.living_area ///
	if year == 2013 | year == 2014, fe vce(cluster pid)

mat coeftab3 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab3 = Logprice_M3 Loginc_M3

mat stattab3 = e(N) \ e(r2)
mat colnames stattab3 = M3
mat rownames stattab3 = N r2

mat_capp coeftab : coeftab0 coeftab1
mat_capp coeftab : coeftab coeftab2
mat_capp coeftab : coeftab coeftab3

mat_capp stattab : stattab0 stattab1
mat_capp stattab : stattab stattab2
mat_capp stattab : stattab stattab3

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/ShortElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/ShortElasticityStat.dta, replace) rownames(xvar) names(col)
```

```{stata ShortIntElasticity, results = "hide", eval = FALSE}
* M0: year >= 2013 without cov
xtreg log_total_g log_price log_pinc_all i.year if year >= 2013 & i_ext_giving == 1, fe vce(cluster pid)

mat coeftab0 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab0 = Logprice_M0 Loginc_M0

mat stattab0 = e(N) \ e(r2)
mat colnames stattab0 = M0
mat rownames stattab0 = N r2

* M1: year >= 2013 with cov
xtreg log_total_g log_price log_pinc_all i.year age sqage i.year##i.educ i.year##i.gender i.year##i.living_area ///
	if year >= 2013 & i_ext_giving == 1, fe vce(cluster pid)

mat coeftab1 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab1 = Logprice_M1 Loginc_M1

mat stattab1 = e(N) \ e(r2)
mat colnames stattab1 = M1
mat rownames stattab1 = N r2

* M2: year == 2013 | 2014 without cov
xtreg log_total_g log_price log_pinc_all i.year if (year == 2013 | year == 2014) & i_ext_giving == 1, fe vce(cluster pid)

mat coeftab2 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab2 = Logprice_M2 Loginc_M2

mat stattab2 = e(N) \ e(r2)
mat colnames stattab2 = M2
mat rownames stattab2 = N r2

* M3: year == 2013 | 2014 with cov
xtreg log_total_g log_price log_pinc_all i.year age sqage i.year##i.educ i.year##i.gender i.year##i.living_area ///
	if (year == 2013 | year == 2014) & i_ext_giving == 1, fe vce(cluster pid)

mat coeftab3 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab3 = Logprice_M3 Loginc_M3

mat stattab3 = e(N) \ e(r2)
mat colnames stattab3 = M3
mat rownames stattab3 = N r2

mat_capp coeftab : coeftab0 coeftab1
mat_capp coeftab : coeftab coeftab2
mat_capp coeftab : coeftab coeftab3

mat_capp stattab : stattab0 stattab1
mat_capp stattab : stattab stattab2
mat_capp stattab : stattab stattab3

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/ShortIntElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/ShortIntElasticityStat.dta, replace) rownames(xvar) names(col)
```

```{stata ShortExtElasticity, results = "hide", eval = FALSE}
** M0: year >= 2013 without cov
xtreg i_ext_giving log_price log_pinc_all i.year if year >= 2013, fe vce(cluster pid)

mat coeftab0 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab0 = Logprice_M0 Loginc_M0

mat stattab0 = e(N) \ e(r2)
mat colnames stattab0 = M0
mat rownames stattab0 = N r2

* price elasticity evaluated at mean
summarize i_ext_giving if year >= 2013
local mu = r(mean)
lincom log_price*(1/`mu')

mat elas1 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas1 = Logprice_M0
mat rownames elas1 = e_b e_se e_pval

* price elasticity evaluated at mean
summarize i_ext_giving if year >= 2013
local mu = r(mean)
lincom log_pinc_all*(1/`mu')

mat elas2 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas2 = Loginc_M0
mat rownames elas2 = e_b e_se e_pval

mat_capp elas : elas1 elas2
mat_rapp coeftab0 : coeftab0 elas

** M1: year >= 2013 with cov
xtreg i_ext_giving log_price log_pinc_all i.year age sqage i.year##i.educ i.year##i.gender i.year##i.living_area ///
	if year >= 2013, fe vce(cluster pid)

mat coeftab1 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab1 = Logprice_M1 Loginc_M1

mat stattab1 = e(N) \ e(r2)
mat colnames stattab1 = M1
mat rownames stattab1 = N r2

* price elasticity evaluated at mean
summarize i_ext_giving if year >= 2013
local mu = r(mean)
lincom log_price*(1/`mu')

mat elas1 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas1 = Logprice_M1
mat rownames elas1 = e_b e_se e_pval

* price elasticity evaluated at mean
summarize i_ext_giving if year >= 2013
local mu = r(mean)
lincom log_pinc_all*(1/`mu')

mat elas2 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas2 = Loginc_M1
mat rownames elas2 = e_b e_se e_pval

mat_capp elas : elas1 elas2
mat_rapp coeftab1 : coeftab1 elas

** M2: year == 2013 | 2014 without cov
xtreg i_ext_giving log_price log_pinc_all i.year if year == 2013 | year == 2014, fe vce(cluster pid)

mat coeftab2 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab2 = Logprice_M2 Loginc_M2

mat stattab2 = e(N) \ e(r2)
mat colnames stattab2 = M2
mat rownames stattab2 = N r2

* price elasticity evaluated at mean
summarize i_ext_giving if year == 2013 | year == 2014
local mu = r(mean)
lincom log_price*(1/`mu')

mat elas1 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas1 = Logprice_M2
mat rownames elas1 = e_b e_se e_pval

* price elasticity evaluated at mean
summarize i_ext_giving if year == 2013 | year == 2014
local mu = r(mean)
lincom log_pinc_all*(1/`mu')

mat elas2 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas2 = Loginc_M2
mat rownames elas2 = e_b e_se e_pval

mat_capp elas : elas1 elas2
mat_rapp coeftab2 : coeftab2 elas

** M3: year == 2013 | 2014 with cov
xtreg i_ext_giving log_price log_pinc_all i.year age sqage i.year##i.educ i.year##i.gender i.year##i.living_area ///
	if year == 2013 | year == 2014, fe vce(cluster pid)

mat coeftab3 = r(table)["b".."pvalue", "log_price".."log_pinc_all"]
mat colnames coeftab3 = Logprice_M3 Loginc_M3

mat stattab3 = e(N) \ e(r2)
mat colnames stattab3 = M3
mat rownames stattab3 = N r2

* price elasticity evaluated at mean
summarize i_ext_giving if year == 2013 | year == 2014
local mu = r(mean)
lincom log_price*(1/`mu')

mat elas1 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas1 = Logprice_M3
mat rownames elas1 = e_b e_se e_pval

* price elasticity evaluated at mean
summarize i_ext_giving if year == 2013 | year == 2014
local mu = r(mean)
lincom log_pinc_all*(1/`mu')

mat elas2 = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
mat colnames elas2 = Loginc_M3
mat rownames elas2 = e_b e_se e_pval

mat_capp elas : elas1 elas2
mat_rapp coeftab3 : coeftab3 elas

mat_capp coeftab : coeftab0 coeftab1
mat_capp coeftab : coeftab coeftab2
mat_capp coeftab : coeftab coeftab3

mat_capp stattab : stattab0 stattab1
mat_capp stattab : stattab stattab2
mat_capp stattab : stattab stattab3

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/ShortExtElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/ShortExtElasticityStat.dta, replace) rownames(xvar) names(col)
```

<!--- Slide
## Robustness Check 2: Result
--->

```{r shapeShortEstimateElasticity, eval = FALSE}
org.tab <- read.dta13("_assets/ShortEstimateElasticity.dta") %>% data.frame()

shape.tab <- org.tab %>% 
	select(xvar, b, se, pvalue, ivf, N) %>% 
	mutate(
		b = case_when(
			pvalue <= 0.01 ~ sprintf("%1.3f***", b),
			pvalue <= 0.05 ~ sprintf("%1.3f**", b),
			pvalue <= 0.1 ~ sprintf("%1.3f*", b),
			TRUE ~ sprintf("%1.3f", b)
		),
		se = sprintf("(%1.3f)", se),
		ivf = if_else(is.na(ivf), "", sprintf("%1.2f", ivf)), 
		N = sprintf("%1d", as.integer(N))
	) %>% 
	select(xvar, b, se, ivf, N) %>% 
	pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
	pivot_wider(values_from = "value", names_from = "xvar") %>% 
	select(-stat) %>% 
	cbind(xvar = c("ln(giving price)", "", "F-stat of IV", "N"), .)

addline <- cbind(
	xvar = c("Individual FE", "Time FE", "Other Controls"),
	model0 = rep("Y", 3),
	model1 = rep("Y", 3),
	model2 = rep("Y", 3),
	model3 = rep("Y", 3)
)

tab <- rbind(shape.tab[1:2,], addline) %>% rbind(shape.tab[3:4,])
```

```{r kableShortEstimateElasticity, include=TRUE, eval = FALSE}
knitr::kable(
    tab,
    format = "latex",
    caption = "Results with 2013 and 2014 Data",
    col.names = c("", "(1)", "(2)", "(3)", "(4)"),
    row.names = FALSE,
    align = "lcccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() %>% 
add_header_above(c("Lag $k$" = 1, " " = 1, "$k = 1$" = 1, "$k = 2$" = 1, "$k = 3$" = 1), escape = FALSE) %>%
add_header_above(c("Model" = 1, "FE" = 1, "Panel IV" = 3)) %>% 
footnote(general_title = "", general = "Notes: $^{*}$ $p < 0.1$, $^{**}$ $p < 0.05$, $^{***}$ $p < 0.01$. Standard errors are clustered at individual level. Other controls are age (its squared value), the interaction between year dummies and education dummies, the interaction between year dummies and gender dummies, and resident area. The instumental variable is $\\\\log(\\\\text{Price}_{ijt}/\\\\text{Price}_{ij(t-k)})$.", threeparttable = TRUE, escape = FALSE)
```

<!--- Slide
## Robustness Check 2: Intensive and Extensive Margin
--->

```{r shapeShortEstimateElasticityIntExt, eval = FALSE}
org.tab <- list(
	intensive = read.dta13("_assets/ShortEstimateElasticityIntensive.dta") %>% data.frame(),
	extensive = read.dta13("_assets/ShortEstimateElasticityExtensive.dta") %>% data.frame()
)

coef.tab <- org.tab %>% 
	purrr::map(function(x)
		x %>% 
		select(xvar, b, se, pvalue, ivf, N) %>% 
		mutate(
			b = case_when(
				pvalue <= 0.01 ~ sprintf("%1.3f***", b),
				pvalue <= 0.05 ~ sprintf("%1.3f**", b),
				pvalue <= 0.1 ~ sprintf("%1.3f*", b),
				TRUE ~ sprintf("%1.3f", b)
			),
			se = sprintf("(%1.3f)", se),
			ivf = if_else(is.na(ivf), "", sprintf("%1.2f", ivf)), 
			N = sprintf("%1d", as.integer(N))
		) %>% 
		select(xvar, b, se, ivf, N) %>% 
		pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
		pivot_wider(values_from = "value", names_from = "xvar") %>% 
		select(-stat) %>% 
		cbind(xvar = c("ln(giving price)", "", "F-stat of IV", "N"), .)
	) %>% 
	reduce(rbind)

elas.tab <- org.tab$extensive %>% 
	mutate(
		e_b = case_when(
			e_pval <= 0.01 ~ sprintf("%1.3f***", e_b),
			e_pval <= 0.05 ~ sprintf("%1.3f**", e_b),
			e_pval <= 0.1 ~ sprintf("%1.3f*", e_b),
			TRUE ~ sprintf("%1.3f", e_b)
		),
		e_se = sprintf("(%1.3f)", e_se)
	) %>% 
	select(xvar, e_b, e_se) %>% 
	pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
	pivot_wider(values_from = "value", names_from = "xvar") %>% 
	select(-stat) %>% 
	cbind(xvar = c("Elasticity", ""), .)

result.tab <- rbind(coef.tab[1:6,], elas.tab) %>% rbind(coef.tab[7:8,])

addline <- cbind(
	xvar = c("Individual FE", "Time FE", "Other Controls"),
	model0 = rep("Y", 3),
	model1 = rep("Y", 3),
	model2 = rep("Y", 3),
	model3 = rep("Y", 3)
)

tab <- rbind(result.tab[1:8,], addline) %>% rbind(result.tab[9:10,])
```

```{r kableShortEstimateElasticityIntExt, include=TRUE, eval = FALSE}
knitr::kable(
    tab,
    format = "latex",
    caption = "Intensive- and Extensive-Margin Elasticity with 2013 and 2014 Data",
    col.names = c("", "(1)", "(2)", "(3)", "(4)"),
    row.names = FALSE,
    align = "lcccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() %>% 
pack_rows("Intensive Margin", 1, 4) %>% 
pack_rows("Extensive Margin", 5, 13) %>% 
add_header_above(c("Lag $k$" = 1, " " = 1, "$k = 1$" = 1, "$k = 2$" = 1, "$k = 3$" = 1), escape = FALSE) %>%
add_header_above(c("Model" = 1, "FE" = 1, "Panel IV with FE" = 3)) %>% 
footnote(general_title = "", general = "Notes: $^{*}$ $p < 0.1$, $^{**}$ $p < 0.05$, $^{***}$ $p < 0.01$. Standard errors are clustered at individual level. Other controls are age (its squared value), the interaction between year dummies and education dummies, the interaction between year dummies and gender dummies, and resident area. The instumental variable is $\\\\log(\\\\text{Price}_{ijt}/\\\\text{Price}_{ij(t-k)})$. The implied extensive-marign price elasticity is evaluated at the sample mean of $D_{ijt}$.", threeparttable = TRUE, escape = FALSE)
```

```{stata kDiffElasticity, results = "hide", eval = FALSE}
forvalues k = 1(1)3 {
    
	di "lag = `k'"
	
	* panel regression
	xtreg log_diff`k'g log_iv`k'price log_diff`k'I diff`k'_age diff`k'_sqage i.year##i.educ i.year##i.gender i.year##i.living_area, ///
		fe vce(cluster pid)
	
	* result of panel regression
	mat coef = r(table)["b".."pvalue","log_iv`k'price".."log_diff`k'I"]
	mat colnames coef = Logdiffprice_M`k' Logdiffinc_M`k'
	
	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r2
	
	if `k' == 1 {
	    mat coeftab = coef
		mat stattab = stat
	}
	else {
	    mat_capp coeftab : coeftab coef
		mat_capp stattab : stattab stat
	}
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/kDiffElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/kDiffElasticityStat.dta, replace) rownames(xvar) names(col)
```

```{stata kDiffIntElasticity, results = "hide", eval = FALSE}
forvalues k = 1(1)3 {
    
	di "lag = `k'"
	
	* panel regression
	xtreg log_diff`k'g log_iv`k'price log_diff`k'I diff`k'_age diff`k'_sqage i.year##i.educ i.year##i.gender i.year##i.living_area ///
		if i_ext_giving == 1, fe vce(cluster pid)
	
	* result of panel regression
	mat coef = r(table)["b".."pvalue","log_iv`k'price".."log_diff`k'I"]
	mat colnames coef = Logdiffprice_M`k' Logdiffinc_M`k'
	
	mat stat = e(N) \ e(r2)
	mat colnames stat = M`k'
	mat rownames stat = N r2
	
	if `k' == 1 {
	    mat coeftab = coef
		mat stattab = stat
	}
	else {
	    mat_capp coeftab : coeftab coef
		mat_capp stattab : stattab stat
	}
}

mat list coeftab
mat list stattab

xsvmat coeftab, saving(_assets/kDiffIntElasticityCoef.dta, replace) rownames(xvar) names(col)
xsvmat stattab, saving(_assets/kDiffIntElasticityStat.dta, replace) rownames(xvar) names(col)
```
