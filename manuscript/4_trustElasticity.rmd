# Political Trust and Price Elasticity

## Estimation of Trust Index

The trust for politicans is time-varying variable because it depends on governments' policies.
We make time-invarying trust index using the fixed effect model.

$$
    \text{Trust}_{ijt} = \text{Trustid}_{ij} + c_j \cdot \lambda_t + \lambda_t + \epsilon_{ijt}.
$$

- $\text{Trust}_{ijt}$: trust for politicians (5-Likert scale)
- $\text{Trustid}_i$: individual fixed effect (**Trust index**)
- $c_j \cdot \lambda_t$ captures local governments' policies effect
- $\lambda_t$ captures the central government policies effect


```{stata EstimateTrustIndex, results = "hide", eval = FALSE}
xtreg trust_politician i.year##i.living_area if year >= 2015, fe
predict orgtrustid, u

xtreg trust_politician i.year##i.living_area if year == 2015 | year == 2016, fe
predict orgparktrustid, u

xtreg trust_politician i.year##i.living_area if year == 2017 | year == 2018, fe
predict orgmoontrustid, u



* make trustid dataset
frame copy default trustdt
frame trustdt: {
	bysort pid: egen trustid = mean(orgtrustid)
	bysort pid: egen parktrustid = mean(orgparktrustid) 
	bysort pid: egen moontrustid = mean(orgmoontrustid)
}
frame trustdt: keep pid trustid parktrustid moontrustid
frame trustdt: duplicates drop
frame trustdt: gen diff = moontrustid - parktrustid
frame trustdt: xtile original5 = trustid, nq(5) 
frame trustdt: xtile park5 = parktrustid, nq(5)
frame trustdt: xtile original3 = trustid, nq(3)
frame trustdt: xtile park3 = parktrustid, nq(3)
frame trustdt: {
	gen lessdiff1 = 0
	replace lessdiff1 = 1 if abs(diff) < 1
	replace lessdiff1 = . if missing(diff)
}
frame trustdt: {
	gen lessdiffhalf = 0
	replace lessdiffhalf = 1 if abs(diff) < 0.5
	replace lessdiffhalf = . if missing(diff)
}
frame trustdt: save "data\shape\trustid.dta", replace
frame drop trustdt
```

## Histrogram of Trust Index

```{stata HistogramTrustid, results = "hide", eval = FALSE}
frame create trustdt
frame trustdt: use "data\shape\trustid.dta", clear

frame trustdt: {
	twoway ///
	(histogram trustid, freq yaxis(2) color(gs10%50) lcolor(black)), ///
	xtitle("Trust index") ///
	graphregion(fcolor(white))
	
	graph export "_assets/HistogramTrustid.pdf", replace
}
```

```{r, include = TRUE, out.width="90%", fig.cap="Histogram of Trust Index"}
knitr::include_graphics(paste(here(), "_assets/HistogramTrustid.pdf", sep= "/"))
```

## Heterogenous Price Elasticity by Political Trust

To see the heterogenous price elasticity by political trust,
We estimated the baseline regression model (5) (see Table \ref{tab:kableEstimateElasticity}),
using sample grouped by the trust index.

- Three quantile groups: we divide units $i$ into the first, second, and third quantile of trust index (1Q, 2Q, and 3Q, respectively).

```{stata SummaryOutcomeByTrustGroup, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

frame copy default plotdt
frame plotdt {
	by year benefit_group original3, sort: egen meang = mean(i_total_giving)
	by year benefit_group original3, sort: egen meanint = mean(i_total_giving) if i_ext_giving == 1
	by year benefit_group original3, sort: egen meanext = mean(i_ext_giving)
}
frame plotdt {
	keep year benefit_group original3 meang meanint meanext
	duplicates drop
	keep if !missing(benefit_group) & !missing(original3) & !missing(meanint)
}

frame plotdt {
	twoway ///
	(connect meang year if benefit_group == 1 & original3 == 1, msymbol(O) color(black))  ///
	(connect meang year if benefit_group == 3 & original3 == 1, msymbol(T) color(black))  ///
	(connect meang year if benefit_group == 1 & original3 == 3, msymbol(O) color(black) lpattern(-))  ///
	(connect meang year if benefit_group == 3 & original3 == 3, msymbol(T) color(black) lpattern(-)), ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlab(2012(1)2018) xtitle("Year") ///
	ytitle("Average donations")  ///
	legend(label(1 "Income {&lt} 1200 (1Q of trust index)") label(2 "Income {&ge} 4600 (1Q of trust index)") ///
		label(3 "Income {&lt} 1200 (3Q of trust index)") label(4 "Income {&ge} 4600 (3Q of trust index)") size(small) cols(1)) ///
	graphregion(fcolor(white%100))

	graph export "_assets/SummaryOutcomeByTrustGroup.pdf", replace
}
frame plotdt {
	twoway ///
	(connect meanint year if benefit_group == 1 & original3 == 1, msymbol(O) color(black))  ///
	(connect meanint year if benefit_group == 3 & original3 == 1, msymbol(T) color(black))  ///
	(connect meanint year if benefit_group == 1 & original3 == 3, msymbol(O) color(black) lpattern(-))  ///
	(connect meanint year if benefit_group == 3 & original3 == 3, msymbol(T) color(black) lpattern(-)), ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlab(2012(1)2018) xtitle("Year") ///
	ytitle("Average donations among donors")  ///
	legend(label(1 "Income {&lt} 1200 (1Q of trust index)") label(2 "Income {&ge} 4600 (1Q of trust index)") ///
		label(3 "Income {&lt} 1200 (3Q of trust index)") label(4 "Income {&ge} 4600 (3Q of trust index)") size(small) cols(1)) ///
	graphregion(fcolor(white%100))

	graph export "_assets/SummaryOutcomeIntensiveByTrustGroup.pdf", replace
}
frame plotdt {
	twoway ///
	(connect meanext year if benefit_group == 1 & original3 == 1, msymbol(O) color(black))  ///
	(connect meanext year if benefit_group == 3 & original3 == 1, msymbol(T) color(black))  ///
	(connect meanext year if benefit_group == 1 & original3 == 3, msymbol(O) color(black) lpattern(-))  ///
	(connect meanext year if benefit_group == 3 & original3 == 3, msymbol(T) color(black) lpattern(-)), ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlab(2012(1)2018) xtitle("Year") ///
	ytitle("Proportion of donors")  ///
	legend(label(1 "Income {&lt} 1200 (1Q of trust index)") label(2 "Income {&ge} 4600 (1Q of trust index)") ///
		label(3 "Income {&lt} 1200 (3Q of trust index)") label(4 "Income {&ge} 4600 (3Q of trust index)") size(small) cols(1)) ///
	graphregion(fcolor(white%100))

	graph export "_assets/SummaryOutcomeExtensiveByTrustGroup.pdf", replace
}

frame drop plotdt
```

```{stata EstimateElasticityByTrustGroup3, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

forvalues i = 1(1)3 {
    
	* subgroup regression
	xtreg log_total_g log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
		if original3 == `i', fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price"]
	mat colnames coef = model`i'
	mat stat = e(N) \ e(r2_a)
	mat colnames stat = model`i'
	mat rownames stat = N r2a
	mat_rapp model`i' : coef stat
	mat model`i' = model`i''
	
	if `i' == 1 {
	    mat tabular = model`i'
	}
	else {
	    mat_rapp tabular : tabular model`i'
	}
	
}

mat list tabular

xsvmat tabular, saving(_assets/EstimateElasticityByTrustGroup3.dta, replace) rownames(xvar) names(col)
```

```{stata EstimateElasticityExtensiveByTrustGroup3, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

forvalues i = 1(1)3 {
    
	* subgroup regression
	xtreg i_ext_giving log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
		if original3 == `i', fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price"]
	mat colnames coef = model`i'
	mat stat = e(N) \ e(r2_a)
	mat colnames stat = model`i'
	mat rownames stat = N r2a
	
	* proportion of donors
	summarize i_ext_giving if original5 == `i'
	local mu = r(mean)

	* implied elasticity
	lincom log_price*(1/`mu')
	mat elas = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
	mat colnames elas = model`i'
	mat rownames elas = e_b e_se e_pval
	
	* regression result for original5 == i
	mat_rapp model`i' : coef elas
	mat_rapp model`i' : model`i' stat
	mat model`i' = model`i''
	
	* combined with previous results
	if `i' == 1 {
	    mat tabular = model`i'
	}
	else {
	    mat_rapp tabular : tabular model`i'
	}
	
}

mat list tabular
xsvmat tabular, saving(_assets/EstimateElasticityExtensiveByTrustGroup3.dta, replace) rownames(xvar) names(col)
```

```{stata EstimateElasticityIntensiveByTrustGroup3, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

forvalues i = 1(1)3 {
    
	* subgroup regression
	xtreg log_total_g log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
		if original3 == `i' & i_ext_giving == 1, fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price"]
	mat colnames coef = model`i'
	mat stat = e(N) \ e(r2_a)
	mat colnames stat = model`i'
	mat rownames stat = N r2a
	mat_rapp model`i' : coef stat
	mat model`i' = model`i''
	
	if `i' == 1 {
	    mat tabular = model`i'
	}
	else {
	    mat_rapp tabular : tabular model`i'
	}
	
}

mat list tabular


xsvmat tabular, saving(_assets/EstimateElasticityIntensiveByTrustGroup3.dta, replace) rownames(xvar) names(col)
```

## Trust Groups: Descriptive Stats

```{r, include = TRUE, out.width="90%", fig.cap="Time Series of Average Donations by Subgroup"}
knitr::include_graphics(paste(here(), "_assets/SummaryOutcomeByTrustGroup.pdf", sep= "/"))
```

## Trust Groups: Descriptive Statis (Extensive Margin)

```{r, include = TRUE, out.width="90%", fig.cap="Time Series of Proportion of Donors by Subgroup"}
knitr::include_graphics(paste(here(), "_assets/SummaryOutcomeExtensiveByTrustGroup.pdf", sep= "/"))
```

## Trust Groups: Descriptive Stats (Intensive Margin)

```{r, include = TRUE, out.width="90%", fig.cap="Time Series of Average Donations among Donors by Subgroup"}
knitr::include_graphics(paste(here(), "_assets/SummaryOutcomeIntensiveByTrustGroup.pdf", sep= "/"))
```

## Trust Groups: Estimation Result

```{r shapeEstimateElasticityByTrustGroup3}
tabset <- list(
	overall = read.dta13("_assets/EstimateElasticityByTrustGroup3.dta") %>% data.frame(),
	intensive = read.dta13("_assets/EstimateElasticityIntensiveByTrustGroup3.dta") %>% data.frame(),
	extensive = read.dta13("_assets/EstimateElasticityExtensiveByTrustGroup3.dta") %>% data.frame()
)

coeftab <- tabset %>% 
	purrr::map(function(x)
		x %>% 
		select(xvar, b, se, pvalue, N) %>% 
		mutate(
			b = case_when(
				pvalue <= 0.01 ~ sprintf("%1.3f***", b),
				pvalue <= 0.05 ~ sprintf("%1.3f**", b),
				pvalue <= 0.1 ~ sprintf("%1.3f*", b),
				TRUE ~ sprintf("%1.3f", b)
			),
			se = sprintf("(%1.3f)", se),
			N = sprintf("%1d", as.integer(N))
		) %>% 
		select(xvar, b, se, N) %>% 
		pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
		pivot_wider(values_from = "value", names_from = "xvar") %>% 
		select(-stat) %>% 
		cbind(xvar = c("ln(giving price)", "", "N"), .)
	) %>% 
	reduce(rbind)

elastab <- tabset$extensive %>% 
	mutate(
		e_b = case_when(
			e_pval <= 0.01 ~ sprintf("%1.3f***", e_b),
			e_pval <= 0.05 ~ sprintf("%1.3f**", e_b),
			e_pval <= 0.1 ~ sprintf("%1.3f*", e_b),
			TRUE ~ sprintf("%1.3f", e_b)
		),
		e_se = sprintf("(%1.3f)", e_se)
	) %>% 
	select(xvar, e_b, e_se) %>% 
	pivot_longer(-xvar, values_to = "value", names_to = "stat") %>% 
	pivot_wider(values_from = "value", names_from = "xvar") %>% 
	select(-stat) %>% 
	cbind(xvar = c("Elasticity", ""), .)

tab <- rbind(coeftab[-9,], elastab) %>% rbind(coeftab[9,])
```

```{r kableEstimateElasticityIntExtByTrustGroup3, include = TRUE}
knitr::kable(
    tab,
    format = "latex",
    caption = "Price Elasticity by Three Quantile Trust Groups",
    col.names = c("", "1Q", "2Q", "3Q"),
    row.names = FALSE,
    align = "lccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling(font_size = 8) %>% 
pack_rows("Overall", 1, 3) %>% 
pack_rows("Intensive Margin", 4, 6) %>% 
pack_rows("Extensive Margin", 7, 11)
```

## Robustness Check

1. Effect of presidential transition on trust index
1. Effect of presidential transition on donation behavior
1. Income and donations are determined simultaneously
1. Last price elasticity
1. Self-selection of receiving tax benefit
1. Transitory and permanent elasticity

## Robustness Check 1: Presidential Transition Effect on Trust

```{stata tTestPresidentTrustid, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

frame copy default tdt
frame tdt {
    keep pid moontrustid parktrustid
	duplicates drop
}

frame tdt: ttest moontrustid == parktrustid

mat tabular = r(mu_1) - r(mu_2) \ r(se) \ r(p)
mat colnames tabular = diff
mat rownames tabular = diff se pval

mat list tabular

xsvmat tabular, saving(_assets/tTestPresidentTrustid.dta, replace) rownames(xvar) names(col)
```

```{r shapetTestPresidentTrustid}
tdt <- read.dta13("_assets/tTestPresidentTrustid.dta") %>% data.frame()
```

- Even though we control time fixed effect when estimating the trust index, we may not rule out the effect of presidential transition on it perfectly.
- To check this point, we repeat estimate the same model using either data in 2015 and 2016 (Park's trust index) or in 2017 and 2018 (Moon's trust index), and obtain the president-specific trust indexs. After that, we test whether the average individual difference b/w these two indexs is statistically different from zero (pair-wise t-test).
- As a result, the average individual difference is `r round(tdt[1,2], 3)` (s.e. = `r round(tdt[2,2],3)`). This difference is not statistically different from zero (p-value = `r round(tdt[3,2], 3)`).

## Robustness Check 2

We check the following two potential concerns

- Presidential transition effect on donation behavior 
- Income and donations are determined simultaneously

To address these problems, we estimate the FE model and Panel IV model with FE where instrument is $\log(\text{Price}_{ijt}/\text{Price}_{ij(t-k)})$ for $k = 1, 2, 3$, using data in 2013 and 2014.

Note that f-statistics of IV is greater than 1000 when we estimate overall elasticity and extensive-margin elasticity, and greater than 400 when we estimate the intensive-margin elasticity.

```{stata ShortEstimateElasticityByTrustGroup3, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

forvalues i = 1(1)3 {
    
	* subgroup regression
	xtreg log_total_g log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
		if original3 == `i' & (year == 2013|year==2014), fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price"]
	mat colnames coef = Q`i'k0
	mat stat = e(N) \ e(r2_a)
	mat colnames stat = Q`i'k0
	mat rownames stat = N r2a
	mat_rapp Q`i'k0 : coef stat
	mat Q`i'k0 = Q`i'k0'
	
	if `i' == 1 {
	    mat tabular = Q`i'k0
	}
	else {
	    mat_rapp tabular : tabular Q`i'k0
	}
	
}

forvalues i = 1(1)3 {
    
	forvalues k = 1(1)3 {
    
		di "lag = `k' with Group Q`i'"
	
		* first stage 
		xtreg log_price diff`k'p log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
			if original3 == `i' & (year == 2013 | year == 2014), fe vce(cluster pid)
	
		* result of first stage
		mat fstage = r(table)["b".."pvalue","diff`k'p"]
		mat fstage = fstage[1,1] \ fstage[3,1]^2
		mat colnames fstage = Q`i'k`k'
		mat rownames fstage = ivcoef ivf
	
		* second stage
		xtivreg log_total_g log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
			(log_price = diff`k'p) if original3 == `i' & (year == 2013 | year == 2014), fe vce(cluster pid)
	
		* result of second stage
		mat coef = r(table)["b".."pvalue","log_price"]
		mat colnames coef = Q`i'k`k'
		mat stat = e(N) \ e(r2_w)
		mat colnames stat = Q`i'k`k'
		mat rownames stat = N r2w
		mat_rapp Q`i'k`k' : coef stat
	
		* combined with first stage result
		mat_rapp Q`i'k`k' : Q`i'k`k' fstage
		mat Q`i'k`k' = Q`i'k`k''
		mat_rapp tabular : tabular Q`i'k`k', miss(.)
		
	}
}


mat list tabular

xsvmat tabular, saving(_assets/ShortElasticityByTrustGroup3.dta, replace) rownames(xvar) names(col)
```

```{stata ShortEstimateElasticityExtensiveByTrustGroup3, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

forvalues i = 1(1)3 {
    
	* subgroup regression
	xtreg i_ext_giving log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
		if original3 == `i' & (year == 2013 | year == 2014), fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price"]
	mat colnames coef = Q`i'k0
	mat stat = e(N) \ e(r2_a)
	mat colnames stat = Q`i'k0
	mat rownames stat = N r2a
	
	* proportion of donors
	summarize i_ext_giving if original3 == `i' & (year == 2013 | year == 2014)
	local mu = r(mean)

	* implied elasticity
	lincom log_price*(1/`mu')
	mat elas = r(estimate) \ r(se) \ ttail(r(df), abs(r(estimate)/r(se)))*2
	mat colnames elas = Q`i'k0
	mat rownames elas = e_b e_se e_pval
	
	* regression result for original5 == i
	mat_rapp Q`i'k0 : coef elas
	mat_rapp Q`i'k0 : Q`i'k0 stat
	mat Q`i'k0 = Q`i'k0'
	
	* combined with previous results
	if `i' == 1 {
	    mat tabular = Q`i'k0
	}
	else {
	    mat_rapp tabular : tabular Q`i'k0
	}
	
}

forvalues i = 1(1)3 {
    
	forvalues k = 1(1)3 {
    
		di "lag = `k' with group Q`i'"
	
		* first stage 
		xtreg log_price diff`k'p log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
			if original3 == `i' & (year == 2013 | year == 2014), fe vce(cluster pid)
	
		* result of first stage
		mat fstage = r(table)["b".."pvalue","diff`k'p"]
		mat fstage = fstage[1,1] \ fstage[3,1]^2
		mat colnames fstage = Q`i'k`k'
		mat rownames fstage = ivcoef ivf
	
		* second stage
		xtivreg i_ext_giving log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
			(log_price = diff`k'p) if original3 == `i' & (year == 2013 | year == 2014), fe vce(cluster pid)
	
		* result of second stage
		mat coef = r(table)["b".."pvalue","log_price"]
		mat colnames coef = Q`i'k`k'
		mat stat = e(N) \ e(r2_w)
		mat colnames stat = Q`i'k`k'
		mat rownames stat = N r2w
	
		*proportion of donors
		summarize i_ext_giving if original3 == `i' & (year == 2013 | year == 2014)
		local mu = r(mean)
	
		*implied elasticity
		lincom log_price*(1/`mu')
		mat elas = r(estimate) \ r(se) \ (1 - normal(abs(r(estimate)/r(se))))*2
		mat colnames elas = Q`i'k`k'
		mat rownames elas = e_b e_se e_pval
	
		* combined with first stage result
		mat_rapp Q`i'k`k' : coef elas
		mat_rapp Q`i'k`k' : Q`i'k`k' stat
		mat_rapp Q`i'k`k' : Q`i'k`k' fstage
		mat Q`i'k`k' = Q`i'k`k''
		mat_rapp tabular : tabular Q`i'k`k', miss(.)
	}
	
}

mat list tabular

xsvmat tabular, saving(_assets/ShortElasticityExtensiveByTrustGroup3.dta, replace) rownames(xvar) names(col)
```

```{stata ShortEstimateElasticityIntensiveByTrustGroup3, results = "hide", eval = FALSE}
merge m:1 pid using "data\shape\trustid.dta"
drop _merge

forvalues i = 1(1)3 {
    
	* subgroup regression
	xtreg log_total_g log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
		if original3 == `i' & i_ext_giving == 1 & (year == 2013 | year == 2014), fe vce(cluster pid)
	
	*matrix of regression result
	mat coef = r(table)["b".."pvalue","log_price"]
	mat colnames coef = Q`i'k0
	mat stat = e(N) \ e(r2_a)
	mat colnames stat = Q`i'k0
	mat rownames stat = N r2a
	mat_rapp Q`i'k0 : coef stat
	mat Q`i'k0 = Q`i'k0'
	
	if `i' == 1 {
	    mat tabular = Q`i'k0
	}
	else {
	    mat_rapp tabular : tabular Q`i'k0
	}
	
}

forvalues i = 1(1)3 {
    
	forvalues k = 1(1)3 {
    
		di "lag = `k' with group Q`k'"
	
		* first stage 
		xtreg log_price diff`k'p log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
			if original3 == `i' & i_ext_giving == 1 & (year == 2013 | year == 2014), fe vce(cluster pid)
	
		* result of first stage
		mat fstage = r(table)["b".."pvalue","diff`k'p"]
		mat fstage = fstage[1,1] \ fstage[3,1]^2
		mat colnames fstage = Q`i'k`k'
		mat rownames fstage = ivcoef ivf
	
		* second stage
		xtivreg log_total_g log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
			(log_price = diff`k'p) if original3 == `i' & i_ext_giving == 1 & (year == 2013 | year == 2014), fe vce(cluster pid)
	
		* result of second stage
		mat coef = r(table)["b".."pvalue","log_price"]
		mat colnames coef = Q`i'k`k'
		mat stat = e(N) \ e(r2_w)
		mat colnames stat = Q`i'k`k'
		mat rownames stat = N r2w
		mat_rapp Q`i'k`k' : coef stat
	
		* combined with first stage result
		mat_rapp Q`i'k`k' : Q`i'k`k' fstage
		mat Q`i'k`k' = Q`i'k`k''
		mat_rapp tabular : tabular Q`i'k`k', miss(.)
	}
	
}

mat list tabular

xsvmat tabular, saving(_assets/ShortElasticityIntensiveByTrustGroup3.dta, replace) rownames(xvar) names(col)
```

```{r shapeShortEstimateElasticityByTrustGroup3}
orgtab <- list(
	overall = read.dta13("_assets/ShortElasticityByTrustGroup3.dta") %>% data.frame(),
	extensive = read.dta13("_assets/ShortElasticityExtensiveByTrustGroup3.dta") %>% data.frame(),
	intensive = read.dta13("_assets/ShortElasticityIntensiveByTrustGroup3.dta") %>% data.frame()
)

shape1 <- list(orgtab$overall, orgtab$intensive) %>% 
	purrr::map(function(x)
		x %>% 
		mutate(
			b = case_when(
				pvalue <= 0.01 ~ sprintf("%1.3f***", b),
				pvalue <= 0.05 ~ sprintf("%1.3f**", b),
				pvalue <= 0.1 ~ sprintf("%1.3f*", b),
				TRUE ~ sprintf("%1.3f", b)
			),
			se = sprintf("(%1.3f)", se),
			N = sprintf("%1d", as.integer(N))
		) %>%
		select(xvar, b, se, N) %>% 
		pivot_longer(-xvar, names_to = "stat", values_to = "val") %>% 
		pivot_wider(names_from = "xvar", values_from = "val") %>% 
		pivot_longer(-stat, names_to = c("group", "lag"), names_pattern = "Q(.)k(.)", values_to = "val") %>% 
		pivot_wider(names_from = "group", values_from = "val") %>% 
		.[order(.$lag),] %>% 
		select(-stat, -lag) %>% 
		cbind(xvar = rep(c("ln(giving price)", "", "N"), 4), .)
	)

shape2 <- orgtab$extensive %>% 
	mutate(
		N = sprintf("%1d", as.integer(N)),
		e_b = case_when(
			e_pval <= 0.01 ~ sprintf("%1.3f***", e_b),
			e_pval <= 0.05 ~ sprintf("%1.3f**", e_b),
			e_pval <= 0.1 ~ sprintf("%1.3f*", e_b),
			TRUE ~ sprintf("%1.3f", e_b)
		),
		e_se = sprintf("(%1.3f)", e_se)
	) %>% 
	select(xvar, e_b, e_se, N) %>% 
	pivot_longer(-xvar, names_to = "stat", values_to = "val") %>% 
	pivot_wider(names_from = "xvar", values_from = "val") %>% 
	pivot_longer(-stat, names_to = c("group", "lag"), names_pattern = "Q(.)k(.)", values_to = "val") %>% 
	pivot_wider(names_from = "group", values_from = "val") %>% 
	.[order(.$lag),] %>% 
	select(-stat, -lag) %>%
	cbind(xvar = rep(c("Implied Elasticity", "", "N"), 4), .) 
```

## Robustness Check 2: Result

```{r tabShortEstimateElasticityByTrustGroup3, include = TRUE}
knitr::kable(
    shape1[[1]],
    format = "latex",
    caption = "Robustness Check of Heterogenous Price Elasiticity by Political Trust",
    col.names = c("", "1Q", "2Q", "3Q"),
    row.names = FALSE,
    align = "lccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling(font_size = 8) %>% 
pack_rows("FE Model", 1, 3) %>% 
pack_rows("Panel IV (k = 1)", 4, 6) %>% 
pack_rows("Panel IV (k = 2)", 7, 9) %>% 
pack_rows("Panel IV (k = 3)", 10, 12) 
```

## Robustness Check 2: Result (Extensive Margin)

```{r tabShortEstimateElasticityExtensiveByTrustGroup3, include = TRUE}
knitr::kable(
    shape2,
    format = "latex",
    caption = "Robustness Check of Heterogenous Extensive-Margin Price Elasiticity by Political Trust",
    col.names = c("", "1Q", "2Q", "3Q"),
    row.names = FALSE,
    align = "lccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling(font_size = 8) %>% 
pack_rows("FE Model", 1, 3) %>% 
pack_rows("Panel IV (k = 1)", 4, 6) %>% 
pack_rows("Panel IV (k = 2)", 7, 9) %>% 
pack_rows("Panel IV (k = 3)", 10, 12) 
```

## Robustness Check 2: Result (Intensive Margin)

```{r tabShortEstimateElasticityIntensiveByTrustGroup3, include = TRUE}
knitr::kable(
    shape1[[2]],
    format = "latex",
    caption = "Robustness Check of Heterogenous Intenstive-Margin Price Elasiticity by Political Trust",
    col.names = c("", "1Q", "2Q", "3Q"),
    row.names = FALSE,
    align = "lccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling(font_size = 8) %>% 
pack_rows("FE Model", 1, 3) %>% 
pack_rows("Panel IV (k = 1)", 4, 6) %>% 
pack_rows("Panel IV (k = 2)", 7, 9) %>% 
pack_rows("Panel IV (k = 3)", 10, 12) 
```
