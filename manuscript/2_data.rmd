# Data
## National Survey of Tax and Benefit (NaSTaB)

- The Korea Institute of Taxation and Finance implements the financial panel survey to study the tax burden of households and the benefits that households receive from goverment.
- The subjects of this survey are general household and household members living in 15 cities and provinces nationwide.
- This survey is based on a face-to-face interview. If it is difficult for investigators to meet subjects, another family member answers on behalf of him.
- Survey items: Annual taxable income (last year), charitable donations (last year), trust for politicians (5-Likert scale), and other covariates (age, education, gender etc.).
- Survey period: 2008 ~ 2019
    - We use survey data after 2013 to focus on tax policy change in 2014.

```{stata dataset, collectcode=TRUE, results = "hide", eval = FALSE}
cd "C:\Users\vge00\Desktop\nastab"  //root path

** ---- ReadData
use "data\shaped.dta", clear

gen price = .
replace price = 1 - mtr if year < 2014
replace price = 1 - 0.15 if year >= 2014
gen log_price = ln(price)
gen log_total_g = ln(i_total_giving + 1)
gen log_pinc_all = ln(lincome + 100000)
replace gender = gender - 1
gen univ = (educ == 3) if !missing(educ)
gen highschool = (educ == 2) if !missing(educ)
gen juniorhigh = (educ == 1) if !missing(educ)
gen sqage = age^2/100

gen benefit_group = .
replace benefit_group = 1 if credit_benefit == 1
replace benefit_group = 2 if credit_neutral == 1
replace benefit_group = 3 if credit_loss == 1

gen now_balance = 0
replace now_balance = 2 if avg_welfare_tax == 1
replace now_balance = 1 if avg_welfare_tax == 2 | avg_welfare_tax == 4
replace now_balance = -1 if avg_welfare_tax == 6 | avg_welfare_tax == 8
replace now_balance = -2 if avg_welfare_tax == 9
replace now_balance = . if missing(avg_welfare_tax)

gen ideal_balance = 0 
replace ideal_balance = 2 if opt_welfare_tax == 1
replace ideal_balance = 1 if opt_welfare_tax == 2 | opt_welfare_tax == 4
replace ideal_balance = -1 if opt_welfare_tax == 6 | opt_welfare_tax == 8
replace ideal_balance = -2 if opt_welfare_tax == 9
replace ideal_balance = . if missing(opt_welfare_tax) 

** ---- LagOperation
tsset pid year

forvalues k = 1(1)3 {
  gen diff`k'G = log_total_g - l`k'.log_total_g
	gen diff`k'G1 = i_ext_giving - l`k'.i_ext_giving
	gen diff`k'p = log_price - l`k'.log_price
	gen diff`k'I = log_pinc_all - l`k'.log_pinc_all
	gen diff`k'_age = age - l`k'.age
	gen diff`k'_sqage = sqage - l`k'.sqage
}

keep if year >= 2012
```

## Time Series of Chariable Giving

```{stata SummaryOutcome, results = "hide", eval = FALSE}
frame copy default avgdt
frame avgdt: by year, sort: egen meanext = mean(i_ext_giving)
frame avgdt: by year, sort: egen meanint = mean(i_total_giving) if i_ext_giving == 1
frame avgdt: keep year meanext meanint
frame avgdt: duplicates drop
frame avgdt: keep if meanint != .

frame avgdt: {
    twoway ///
	(bar meanext year, color(gs10) lcolor(black) barwidth(0.9) yaxis(1)) ///
	(connected meanint year, color(blue) yaxis(2)), ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	yscale(range(0 .) axis(1)) ///
	yscale(range(0 .) axis(2)) ///
	ylabel(0(.05).2, axis(1)) ///
	ylabel(0(50)200, axis(2)) ///
	ytitle("Proportion of donors", axis(1)) ///
	ytitle("Average donations among donors", axis(2)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	legend(label(1 "Extensive margin") label(2 "Intensive margin")) ///
	graphregion(fcolor(white))

  graph export "_assets/SummaryOutcome.pdf", replace
}
```

```{r, include = TRUE, out.width="90%", fig.cap="Proportion of Donors and Average Donations among Donors"}
knitr::include_graphics(paste(here(), "_assets/SummaryOutcome.pdf", sep= "/"))
```

## Summary Statistics of Covariates

```{stata SummaryCovariate, results = "hide", eval = FALSE}
matrix sumcov = J(7, 7, .)

label variable gender "Female"
label variable age "Age"
label variable lincome "Annual taxable income"
label variable univ "University graduate"
label variable highschool "High School Graduate"
label variable pid "#.Respondents"
label variable hhid "#.Households"

local j = 0
foreach v in gender age lincome univ highschool {
    local k = 1
	local j = `++j'
	di "j = `j'"
	forvalues y = 2012(1)2018 {
	    di "k = `k'"
		summarize `v' if year == `y'
		matrix sumcov[`j',`k'] = r(mean)
		local k = `++k'
	}
}

local k = 1
forvalues y = 2012(1)2018 {
    di = "k = `k'"
	summarize pid if year == `y'
	matrix sumcov[6, `k'] = r(N)
	local k = `++k'
}

frame copy default temp
frame temp: keep year hhid
frame temp: duplicates drop
local k = 1
forvalues y = 2012(1)2018 {
    di = "k = `k'"
	frame temp: summarize hhid if year == `y'
	matrix sumcov[7, `k'] = r(N)
	local k = `++k'
}

matrix rownames sumcov = gender age lincome univ highschool pid hhid 
xsvmat sumcov, saving(_assets/SummaryCovariate.dta) rownames(xvar)
```

```{r shapeSummaryCovariate}
tabsum <- read.dta13("_assets/SummaryCovariate.dta") %>% data.frame()
tabsum <- tabsum %>% 
	mutate_at(vars(-xvar), 
		list(
			~case_when(
				xvar == "pid" | xvar == "hhid" ~ sprintf("%1d", as.integer(.)),
				TRUE ~ sprintf("%1.2f", .)
			)
		)
	) %>% 
	mutate(
		xvar = recode(
			xvar,
			"gender" = "Female",
			"age" = "Age",
			"lincome" = "Annual taxable income",
			"univ" = "University graduate",
			"highschool" = "High school graduate",
			"pid" = "#.Respondents",
			"hhid" = "#.Households" 
		)
	)
```

```{r kableSummaryCovariate, include=TRUE}
knitr::kable(
    tabsum[,1:5],
    format = "latex",
    caption = "Summary Statistics of Covariates",
    col.names = c("", "2012", "2013", "2014", "2015"),
    row.names = FALSE,
    align = "lccccccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() 
```

## Summary Statistics of Covariates (Cont'd)

```{r kableSummaryCovariate2, include=TRUE}
knitr::kable(
    tabsum[,c(1,6:8)],
    format = "latex",
    caption = "Summary Statistics of Covariates (Continued)",
    col.names = c("", "2016", "2017", "2018"),
    row.names = FALSE,
    align = "lccccccc", 
    booktabs = TRUE, escape = TRUE, linesep = ""
) %>% 
kable_styling() 
```


## What is Giving Price?

Consider allocation between private consumptions ($x_i$) and charitable giving ($g_i$).
Let $y_i$ be pre-tax total income.
Then, the budget constraint is 

$$
    x_i + g_i = y_i - T_i(y_i, g_i),
$$

where $T_i$ is tax amount depending on the pre-tax income and charitable giving.

## Determination of Tax Amount

Tax deduction reduces taxable income by giving, that is,

$$
    T_i = \tau(y_i - g_i) \cdot (y_i - g_i),
$$

where $\tau(\cdot)$ is the marginal income tax rate which is determined by $y_i - g_i$.

Tax credit reduces tax amount directly, that is,

$$
    T_i = \tau(y_i)\cdot y_i - m g_i,
$$

where $m \in [0, 1]$ is the tax credit rate.

## Derive Giving Price

Under the tax deduction system, the budget constraint is 

$$
    x_i + [1 - \tau(y_i - g_i)]g_i = [1 - \tau(y_i - g_i)] y_i.
$$

Thus, the giving price of tax deduction system is $p_i^{d} = 1 - \tau(y_i - g_i)$.

Under the tax credit system, the budget constraint is 

$$
    x_i + (1 - m) g_i = [1 - \tau(y_i)] y_i.
$$

Thus, the giving price of tax credit system is $p_i^c = 1 - m$.

## Construct Giving Price

In the South Korea, the tax policy about charitable giving drastically changed in 2014.

- tax deduction (before 2014): $\text{Price}_i = 1 - \tau(y_i - g_i)$
    - the giving price is endogenous because people can manipulate $\tau(y_i - g_i)$ using the charitable giving $g_i$. Since this problem is caused by *last* donations, we use the giving price applying to the *first* donations (**first price**). The first price is calculate by $\tau(y_i)$ where $y_i$ is the annual taxable income reported in the NaSTaB.
- tax credit (after 2014): $\text{Price}_i = 1 - m$
    - In the South Korea, the tax credit rate determines exogeneity, $m = 0.15$.

## Income Distribution and Giving Price

```{stata SummaryPriceChange, results = "hide", eval = FALSE}
frame copy default sump
frame sump: keep if year == 2013
frame sump: keep lincome price

frame sump: {
    twoway ///
	(line price lincome, yaxis(1) connect(stairstep) sort(lincome) lcolor(blue)) ///
	(histogram lincome, freq yaxis(2) color(gs10%50) lcolor(black)), ///
	yline(0.85, lcolor(red) lpattern(-)) ///
	xline(1200, lcolor(black) lpattern(-))  ///
	xline(4600, lcolor(black) lpattern(-))  ///
	ytitle("Giving Price", axis(1)) ///
	ytitle("Count", axis(2)) ///
	xlabel(1200 4600 8800 30000) ///
	xtitle("Annual taxable income (unit: 10,000KRW)")  ///
	legend(label(1 "Giving Price in 2013")) ///
	graphregion(fcolor(white))

  graph export "_assets/SummaryPriceChange.pdf", replace
}
```

```{r, include = TRUE, out.width="90%", fig.cap="Income Distribution and Giving Price in 2013"}
knitr::include_graphics(paste(here(), "_assets/SummaryPriceChange.pdf", sep= "/"))
```

## Time Series of Average Donations By Benefit Group 

```{stata SummaryOutcomeByBenefitGroup, results ="hide", eval = FALSE}
frame copy default plotdt
frame plotdt {
	gen benefit_group = .
	replace benefit_group = 1 if credit_benefit == 1
	replace benefit_group = 2 if credit_neutral == 1
	replace benefit_group = 3 if credit_loss == 1
	
	by year benefit_group, sort: egen meang = mean(i_total_giving)
	by year benefit_group, sort: egen meanint = mean(i_total_giving) if i_ext_giving == 1
	by year benefit_group, sort: egen meanext = mean(i_ext_giving)
}
frame plotdt: keep year benefit_group meang meanint meanext
frame plotdt: duplicates drop
frame plotdt: keep if !missing(meanint) & !missing(benefit_group)

frame plotdt {
	twoway ///
	(connected meang year if benefit_group == 1, msymbol(O) color(black))  ///
	(connected meang year if benefit_group == 2, msymbol(T) color(black))  ///
	(connected meang year if benefit_group == 3, msymbol(S) color(black)),  ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	ytitle("Average Total Donations")  ///
	legend(label(1 "Income {&lt} 1200") label(2 " Income in [1200, 4600)") label(3 "Income {&ge} 4600"))  ///
	graphregion(fcolor(white))

	graph export "_assets/AverageDonationsByBenefitGroup.pdf", replace
}

frame plotdt {
	twoway ///
	(connected meanint year if benefit_group == 1, msymbol(O) color(black))  ///
	(connected meanint year if benefit_group == 2, msymbol(T) color(black))  ///
	(connected meanint year if benefit_group == 3, msymbol(S) color(black)),  ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	ytitle("Average Total Donations among Donors")  ///
	legend(label(1 "Income {&lt} 1200") label(2 " Income in [1200, 4600)") label(3 "Income {&ge} 4600"))  ///
	graphregion(fcolor(white))

	graph export "_assets/IntensiveByBenefitGroup.pdf", replace
}

frame plotdt {
	twoway ///
	(connected meanext year if benefit_group == 1, msymbol(O) color(black))  ///
	(connected meanext year if benefit_group == 2, msymbol(T) color(black))  ///
	(connected meanext year if benefit_group == 3, msymbol(S) color(black)),  ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	ytitle("Proportion of Donors")  ///
	legend(label(1 "Income {&lt} 1200") label(2 " Income in [1200, 4600)") label(3 "Income {&ge} 4600"))  ///
	graphregion(fcolor(white))

	graph export "_assets/ExtensiveByBenefitGroup.pdf", replace
}

```

```{r, include = TRUE, out.width="90%", fig.cap="Time Series of Average Donations by Benefit Group"}
knitr::include_graphics(paste(here(), "_assets/AverageDonationsByBenefitGroup.pdf", sep= "/"))
```

## Time Series of Extensive Margin by Benfit Group

```{r, include = TRUE, out.width="90%", fig.cap="Time Series of Proportion of Donors by Benefit Group"}
knitr::include_graphics(paste(here(), "_assets/ExtensiveByBenefitGroup.pdf", sep= "/"))
```

## Time Series of Intensive Margin by Benfit Group

```{r, include = TRUE, out.width="90%", fig.cap="Time Series of Average Donations among Donors by Benefit Group"}
knitr::include_graphics(paste(here(), "_assets/IntensiveByBenefitGroup.pdf", sep= "/"))
```