# Data
## National Survey of Tax and Benefit (NaSTaB)

- The Korea Institute of Taxation and Finance implements the financial panel survey to study the tax burden of households and the benefits that households receive from goverment.
- The subjects of this survey are general household and household members living in 15 cities and provinces nationwide.
- This survey is based on a face-to-face interview. If it is difficult for investigators to meet subjects, another family member answers on behalf of him.
- Survey items: Annual taxable income (last year), charitable donations (last year), trust for politicians (5-Likert scale), and other covariates (age, education, gender etc.).
- Survey period: 2008 ~ 2019
    - We use survey data after 2013 to focus on tax policy change in 2014.


```{stata dataset, collectcode=TRUE, results = "hide", eval = FALSE}
use "data\shaped.dta", clear
tsset pid year

gen log_price = ln(price)
gen log_lprice = ln(lprice)
gen log_iv1price = ln(iv1price)
gen log_iv2price = ln(iv2price)
gen log_iv3price = ln(iv3price)
gen log_total_g = ln(i_total_giving + 1)
gen log_pinc_all = ln(lincome + 100000)

forvalues i = 1(1)3 {
	gen lag`i'_log_total_g = l`i'.log_total_g
	gen lag`i'_log_pinc_all = l`i'.log_pinc_all
	gen lag`i'_age = l`i'.age
	gen lag`i'_sqage = l`i'.sqage
	
	gen log_diff`i'g = log_total_g - lag`i'_log_total_g
	gen log_diff`i'I = log_pinc_all - lag`i'_log_pinc_all
	gen diff`i'_age = age - lag`i'_age
	gen diff`i'_sqage = sqage - lag`i'_sqage
}

keep if year >= 2012 & age >= 24

** ---- merged with balancedt
merge m:1 pid using "data\shape\balanceid.dta"
drop _merge

foreach v in log_price log_lprice log_iv1price log_iv2price log_iv3price {
    gen `v'_int2 = `v' * (balance3 == 2) if !missing(balance3)
	gen `v'_int3 = `v' * (balance3 == 3) if !missing(balance3)
}
```

## Time Series of Chariable Giving

```{stata SummaryOutcome, results = "hide", eval = FALSE}
frame copy default avgdt
frame avgdt: by year, sort: egen meanext = mean(i_ext_giving)
frame avgdt: by year, sort: egen meanint = mean(i_total_giving) if i_ext_giving == 1
frame avgdt: keep year meanext meanint
frame avgdt: duplicates drop
frame avgdt: keep if meanint != .

frame avgdt: {
    twoway ///
	(bar meanext year, color(gs10) lcolor(black) barwidth(0.9) yaxis(1)) ///
	(connected meanint year, color(blue) yaxis(2)), ///
	xline(2013.5, lcolor(red) lpattern(-)) ///
	yscale(range(0 .) axis(1)) ///
	yscale(range(0 .) axis(2)) ///
	ylabel(0(.05).2, axis(1)) ///
	ylabel(0(50)200, axis(2)) ///
	ytitle("Proportion of donors", axis(1)) ///
	ytitle("Average donations among donors (10,000KRW)", axis(2)) ///
	xlabel(2012(1)2018) xtitle("Year")  ///
	legend(label(1 "Extensive margin") label(2 "Intensive margin")) ///
	graphregion(fcolor(white))

  graph export "_assets/SummaryOutcome.pdf", replace
}
```

```{r, include = TRUE, out.width="90%", fig.cap="Proportion of Donors and Average Donations among Donors"}
knitr::include_graphics(paste(here(), "_assets/SummaryOutcome.pdf", sep= "/"))
```

## Summary Statistics

```{stata SummaryCovariate, results = "hide", eval = FALSE}
local k = 0
foreach v in lincome price i_total_giving i_ext_giving now_balance ideal_balance age gender univ highschool juniorhigh {
	local k = `++k'
	sum `v', detail
	mat `v' = r(N) \ r(mean) \ r(sd) \ r(min) \ r(p25) \ r(p50) \ r(p75) \ r(max)
	mat colnames `v' = `v'
	mat rownames `v' = N mean sd min p25 median p75 max
	
	if `k' == 1 {
	    mat tabular = `v'
	}
	else {
	    mat_capp tabular : tabular `v'
	}
}

mat tabular = tabular'
mat list tabular
xsvmat tabular, saving(_assets/SummaryCovariate.dta, replace) rownames(xvar) names(col)
```

```{r shapeSummaryCovariate}
tabsum <- read.dta13("_assets/SummaryCovariate.dta") %>% data.frame()
tabsum <- tabsum %>% 
	mutate(
		xvar = recode(
			xvar,
			"lincome" = "Annual taxable income (unit: 10,000KRW)",
			"price" = "Giving Price",
			"i_total_giving" = "Annual charitable giving (unit: 10,000KRW)",
			"i_ext_giving" = "dummy of Donation > 0",
			"now_balance" = "Current Tax-Welfare Balance",
			"ideal_balance" = "Ideal Tax-Welfare Balance",
			"age" = "Age",
			"gender" = "Female dummy",
			"univ" = "University graduate",
			"highschool" = "High school graduate",
			"juniorhigh" = "Junior high school graduate"
		)
	)
```

```{r kableSummaryCovariate, include=TRUE}
knitr::kable(
    tabsum,
    format = "latex",
    caption = "Summary Statistics",
    col.names = c("", "N", "Mean", "Std.Dev.", "Min", "p25", "p50", "p75", "Max"),
    row.names = FALSE,
    align = "lcccccccc", 
    booktabs = TRUE, escape = TRUE, linesep = "",
		digits = 3
) %>% 
kable_styling(font_size = 8) %>% 
pack_rows("Income and Giving Price", 1, 2) %>% 
pack_rows("Charitable Donations", 3, 4) %>% 
pack_rows("Government Efficiency", 5, 6) %>% 
pack_rows("Individual Characteristics", 7, 11)
```


## What is Giving Price?

Consider allocation between private consumptions ($x_i$) and charitable giving ($g_i$).
Let $y_i$ be pre-tax total income.
Then, the budget constraint is 

$$
    x_i + g_i = y_i - T_i(y_i, g_i),
$$

where $T_i$ is tax amount depending on the pre-tax income and charitable giving.

## Determination of Tax Amount

Tax deduction reduces taxable income by giving, that is,

$$
    T_i = \tau(y_i - g_i) \cdot (y_i - g_i),
$$

where $\tau(\cdot)$ is the marginal income tax rate which is determined by $y_i - g_i$.

Tax credit reduces tax amount directly, that is,

$$
    T_i = \tau(y_i)\cdot y_i - m g_i,
$$

where $m \in [0, 1]$ is the tax credit rate.

## Derive Giving Price

Under the tax deduction system, the budget constraint is 

$$
    x_i + [1 - \tau(y_i - g_i)]g_i = [1 - \tau(y_i - g_i)] y_i.
$$

Thus, the giving price of tax deduction system is $p_i^{d} = 1 - \tau(y_i - g_i)$.

Under the tax credit system, the budget constraint is 

$$
    x_i + (1 - m) g_i = [1 - \tau(y_i)] y_i.
$$

Thus, the giving price of tax credit system is $p_i^c = 1 - m$.

## Construct Giving Price

In the South Korea, the tax policy about charitable giving drastically changed in 2014.

- tax deduction (before 2014): $\text{Price}_i = 1 - \tau(y_i - g_i)$
    - the giving price is endogenous because people can manipulate $\tau(y_i - g_i)$ using the charitable giving $g_i$. Since this problem is caused by *last* donations, we use the giving price applying to the *first* donations (**first price**). The first price is calculate by $\tau(y_i)$ where $y_i$ is the annual taxable income reported in the NaSTaB.
- tax credit (after 2014): $\text{Price}_i = 1 - m$
    - In the South Korea, the tax credit rate determines exogeneity, $m = 0.15$.

## Income Distribution and Giving Price

```{stata SummaryPriceChange, results = "hide", eval = FALSE}
frame copy default sump
frame sump: keep if year == 2013
frame sump: keep lincome price

frame sump: {
    twoway ///
	(line price lincome, yaxis(1) connect(stairstep) sort(lincome) lcolor(blue)) ///
	(histogram lincome, freq yaxis(2) color(gs10%50) lcolor(black)), ///
	yline(0.85, lcolor(red) lpattern(-)) ///
	xline(1200, lcolor(black) lpattern(-))  ///
	xline(4600, lcolor(black) lpattern(-))  ///
	ytitle("Giving Price", axis(1)) ///
	ytitle("Count", axis(2)) ///
	xtitle("Annual taxable income (10,000KRW)")  ///
	xlabel(1200 4600 8800 30000) ///
	legend(label(1 "Giving Price in 2013")) ///
	graphregion(fcolor(white))

  graph export "_assets/SummaryPriceChange.pdf", replace
}
```

```{r, include = TRUE, out.width="90%", fig.cap="Income Distribution and Giving Price in 2013"}
knitr::include_graphics(paste(here(), "_assets/SummaryPriceChange.pdf", sep= "/"))
```


## Empirical Strategy

Our baseline regression equation is 

$$
    \log(\text{Giving}_{ijt}) = 
    \alpha_i + \beta_1 \log(\text{Price}_{ijt}) + \delta X_{ijt} + \lambda_t + \epsilon_{ijt}.
$$

- $\log(\text{Giving}_{ijt})$ is logarithm of individual $i$'s charitable giving in year $t$.
- $\log(\text{Price}_{ijt})$ is logarithm of individual $i$'s giving price in year $t$.
- $\beta_1$ represents the price elasticity of giving.
- $\alpha_i$ and $\lambda_t$ are individual and time fixed effect, respectively.

## Intensive Margin and Extensive Margin

Let $D_{ijt}$ be a dummy variable taking 1 if individual $i$ whose resident area $j$ in year $t$ donate in year $t$

- Intensive margin: Estiamte $\beta_1$ where outcome variable is $\log(\text{Giving}_{ijt})$, using units with $D_{ijt} = 1$.
- Extensive margin: Estimate $\beta_1$ where outcome variable is $D_{ijt}$.
	- Extensive-margin price elasticity can be calculated by $\beta_1/\bar{D}$ where $\bar{D}$ is the sample mean of $D_{ijt}$.

Covariates in each column corresponds to a column in a previous slide.
