---
title: "Monetary Donations, Government Expenditure, and Giving Prices (Tentative)"
date: "`r format(Sys.time(), '%Y/%m/%d')`"
output:
  html_document:
    toc: TRUE
    toc_float: TRUE
---

<style>

table, td, th {
  border: none;
  padding-left: 1em;
  padding-right: 1em;
  min-width: 50%;
  margin-left: auto;
  margin-right: auto;
  margin-top: 1em;
  margin-bottom: 1em;
}

</style>



# Crowding-out Effect of Governement Expenditure

地方政府が住民の利益になるような政策を実施することをその地域での公共財投資とみなして、
地方政府の支出の増加による金銭的寄付のクラウディングアウトの程度を分析する。

## データと分析手法

**National Survey of Tax and Benefit (NaSTaB)**：ある年$t$において地域$j$に住む個人$i$

- `log_total_g`：個人$i$の寄付総額の対数値（$\log D_{ijt}$）
- `log_pinc_all`：個人$i$の$t$年の労働所得の対数値（$\log I_{ijt}$）
- `log_price`：個人$i$の$t$年の寄付価格の対数値（$\log P_{ijt}$）
    - 2014年以前は1から年間労働所得で計算した限界所得税率（`mtr`）を引いた値（所得控除制度）
	- 2014年以降は1から共通の税額控除率（15\%）を引いた値（税額控除制度）
- `welfare_level_3scale`：$t$年調査の「今の納税に対して政府の公共政策は十分か」という質問に対する5段階の回答を3段階に集約する

**ソースはどこ？**：ある年$t$における地域$j$の地方政府統計

- `log_PPP_pubbdg`：$t$年における地方政府$j$の社会福祉分野に関する一人当たり支出の対数値（$\log G_{jt}$）
	- 社会福祉分野：基礎生活保障、脆弱階層支援、保育·家族及び女性、老人·青少年、労働、報勲、住宅、社会福祉一般
	- `sqlog_PPP_pubpdg`：この変数の二乗項、$(\log G_{jt})^2$
- `log_PPP_healthbdg`：$t$年における地方政府$j$の保健分野に関する一人当たり支出の対数値（$\log G_{jt}$）
	- 保健分野：保健医療、食品医薬安全
	- `sqlog_PPP_healthpdg`：この変数の二乗項、$(\log G_{jt})^2$
- `log_PPP_pub_j`：$t$年における地域$j$以外の地方政府の社会福祉分野に関する一人当たり平均支出の対数値（$\log G_{-jt}$）
	- `sqlog_PPP_pub_j`：この変数の二乗項、$(\log G_{-jt})^2$
- `log_PPP_health_j`：$t$年における地域$j$以外の地方政府の保健分野に関する一人当たり平均支出の対数値（$\log G_{-jt}$）
	- `sqlog_PPP_health_j`：この変数の二乗項、$(\log G_{-jt})^2$


```{stata dataset, collectcode=TRUE, results = "hide", eval = TRUE}
cd "C:\Users\vge00\Desktop\nastab"  //root path

eststo clear
use "data\shaped.dta", clear

gen price = .
replace price = 1 - mtr if year < 2014
replace price = 1 - 0.15 if year >= 2014

gen log_price = ln(price)
gen log_total_g = ln(i_total_giving + 1)
gen log_pinc_all = ln(lincome + 100000)
gen log_PPP_pubbdg = ln(PPP_pubbdg + 1)
gen sqlog_PPP_pubpdg = log_PPP_pubbdg^2
gen log_PPP_healthbdg = ln(PPP_healthbdg + 1)
gen sqlog_PPP_healthpdg = log_PPP_healthbdg^2

xtset pid
```

**Two-way fixed effect model**

$$ 
	\log D_{ijt} = \beta_0 + \beta_1 \log G_{jt} + \beta_2 (\log G_{jt})^2 + \gamma_1 \log I_{ijt} + \gamma_2 \log P_{ijt} + \delta X_{ijt} + \mu_i + \lambda_t + \epsilon_{ijt}. 
$$

- $(\mu_i, \lambda_t)$：個人$i$と時点$t$の固定効果
- $\epsilon_{ijt}$：誤差項
- $X_{ijt}$：個人$i$の共変量
	- `age`：$t$年における個人$i$の年齢
	- `i.year##i.educ`：個人$i$の教育年数と$t$年ダミーの交差項
	- `i.year##i.gender`：個人$i$の性別と$t$年ダミーの交差項
	- `i.living_area`：$t$年における個人$i$の居住地域$j$のダミー変数

このモデルから、寄付の政府支出に対する弾力性$\eta_{GD}$は次のように計算される。

$$ \eta_{GD} = \beta_1 + 2 \beta_2 \log G_{jt}. $$

## Baseline Results

ベースラインの結果として、$\log G_{jt}$に社会福祉分野の政府支出を用いた結果を示す。

- モデル(1)～(4)：地域ダミー`i.living_area`を加えていないとき、弾性値（`log_PPP_pubbdg`の係数）は約0.1\%であるが、統計的に非有意である（もしくは10\%水準で統計的に有意）。
- モデル(5)：弾性値は0.839\%であり、統計的に有意である。すなわち、政府支出の増加は市民の寄付を増やす（**クラウドイン**）。
- モデル（６）：弾力性に異質性があることを示している。弾力性の予測値は$$ \hat{\eta}_{DG} = 7.729 - 2 * 0.529 * \log G_{jt} $$で得られる。


```{stata baseline1, results = "hide", eval = TRUE}
/*******************************************************************************

Baseline results using budge for social welfare

*******************************************************************************/

* intensive + extensive margin (public services)

* model 1: baseline
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 2: + age
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 3: + education-time dummies
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 4: + gender-time dummies
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 5: + living_area
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 6: + squared logged government budge
eststo: xtreg log_total_g log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

gen pred_e_pub = _b[log_PPP_pubbdg] + 2 * _b[sqlog_PPP_pubpdg] * log_PPP_pubbdg

esttab using "_assets\baseline1.html", ///
	replace se keep(log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all)  ///
	order(log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all) ///
	title("Social Welfare Expenditure and Monetary Donations") 
eststo clear
```

```{r, echo = FALSE}
library(here)
htmltools::includeHTML(paste(here(), "_assets/baseline1.html", sep= "/"))
```

- 弾性値の予測値と一人当たり政府支出の関係を示した図を以下に示した。
	- 政府支出の増加に伴い、寄付の弾力性はマイナス方向に向かう。すなわち、**政府支出の増大に伴い、金銭的寄付はクラウドインからクラウドアウトに向かう**。

```{stata plot baseline1, results = "hide", eval = TRUE}
* model 6: + squared logged government budge
eststo: xtreg log_total_g log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

gen pred_e_pub = _b[log_PPP_pubbdg] + 2 * _b[sqlog_PPP_pubpdg] * log_PPP_pubbdg

keep pred_e_pub PPP_pubbdg year
keep if year >= 2012
duplicates drop

twoway	(scatter pred_e_pub PPP_pubbdg, mcolor(%30)), ///
        yline(0, lpattern(dash) lcolor(red)) ///
        xtitle("Social Welfare Budget per capita")  ///
        ytitle("Predicted Elasticity")  ///
        title("Predicted Budget-Elasticity of Donations")  
graph export "_assets\baseline1.png", width(700) replace
```

```{r, echo = FALSE, out.width="100%", fig.cap="社会福祉予算と弾力性の散布図"}
knitr::include_graphics(paste(here(), "_assets/baseline1.png", sep= "/"))
```

## Government Trust and Crowding-in effect

政府支出の規模が小さいほど、寄付のクラウドインが生じるという結果を**政治家への信頼**で説明することを試みる。

**メカニズム**：政府支出の規模が小さいとき、個人は政府規模の増大に伴い、自身の寄付を増やそうと協力的な行動をとろうとする。しかし、政府支出が十分に大きいとき、政府規模が増大しても、個人は協力的な行動をとるインセンティブがない。よって、クラウドアウトが顕著に現れる。

このメカニズムを検討するために、政治家への信頼度`trust_politician`（`year >= 2015`で調査されている）を用いる。
政治家への信頼が薄いならば、政府の社会福祉政策に協力しようという気が起きないので、
標準的な理論通りの結果が得られる（政府支出の増大に伴い、金銭的寄付はクラウドアウトから漸近的にゼロクラウドアウトに向かう）。
しかしながら、政治家への信頼が強いならば、政府の社会福祉政策に協力しようという気が起きるので、
政府支出の増大に伴い、金銭的寄付はクラウドインから漸近的にゼロクラウドインに向かう。

推計式は

$$ \log D_{ijt} = \beta_0 + \beta_1 \log G_{jt} * \mathrm{Trust}_{it} + \beta_2 (\log G_{jt})^2 * \mathrm{Trust}_{it} + \gamma_1 \log I_{ijt} + \gamma_2 \log P_{ijt} + \delta X_{ijt} + \mu_i + \lambda_t + \epsilon_{ijt}. $$

```{stata trust_elast, results = "hide", eval = TRUE}
/*******************************************************************************

Government Trust and Crowd-in
Since trust for politicians can be observed if year >= 2015
We omit price of charitable giving

*******************************************************************************/

* intensive + extensive margin (public services)

eststo: xtreg log_total_g c.log_PPP_pubbdg##i.trust_politician c.sqlog_PPP_pubpdg##i.trust_politician ///
	log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2015, fe vce(cluster pid)

esttab using "_assets\trust_elast.html", ///
	replace se ///
	keep(log_PPP_pubbdg sqlog_PPP_pubpdg ///
	2.trust_politician#c.log_PPP_pubbdg ///
	3.trust_politician#c.log_PPP_pubbdg ///
	4.trust_politician#c.log_PPP_pubbdg ///
	5.trust_politician#c.log_PPP_pubbdg ///
	2.trust_politician#c.sqlog_PPP_pubpdg ///
	3.trust_politician#c.sqlog_PPP_pubpdg ///
	4.trust_politician#c.sqlog_PPP_pubpdg ///
	5.trust_politician#c.sqlog_PPP_pubpdg)  ///
	title("Political Trust and Crowd-in") ///
	label star(* 0.1 ** 0.05 *** 0.01)
eststo clear
```

```{r, echo = FALSE}
htmltools::includeHTML(paste(here(), "_assets/trust_elast.html", sep= "/"))
```

交差項が統計的に10\%水準以下で有意である`trust_politician = 1, 2, 3`についての弾力性の予測性を以下にプロットした。

- 政治家への信頼が全くないグループ（`Lowest trust`）について、政府支出の増大に伴い、クラウドアウトからクラウドインに向かう
- 政治家への信頼が若干にあるグループ（`Lower trust`）とニュートラルなグループ（`Nuetral`）について、政府支出の増大に伴い、クラウドインからクラウドアウトに向かう

おそらく、**政治家への信頼に基づく政府に対する協力度が強いほど、標準的な理論から外れる行動を取る**

```{stata plot trust_elast, results = "hide"}
eststo: xtreg log_total_g c.log_PPP_pubbdg##i.trust_politician c.sqlog_PPP_pubpdg##i.trust_politician ///
	log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2015, fe vce(cluster pid)

gen pred_e_pub_sep = .
replace pred_e_pub_sep = _b[log_PPP_pubbdg] + 2 * _b[sqlog_PPP_pubpdg] * log_PPP_pubbdg if trust_politician == 1
replace pred_e_pub_sep = (_b[log_PPP_pubbdg] + _b[2.trust_politician#c.log_PPP_pubbdg]) + ///
	2 * (_b[sqlog_PPP_pubpdg] + _b[2.trust_politician#c.sqlog_PPP_pubpdg]) * log_PPP_pubbdg if trust_politician == 2
replace pred_e_pub_sep = (_b[log_PPP_pubbdg] + _b[3.trust_politician#c.log_PPP_pubbdg]) + ///
	2 * (_b[sqlog_PPP_pubpdg] + _b[3.trust_politician#c.sqlog_PPP_pubpdg]) * log_PPP_pubbdg if trust_politician == 3

keep pred_e_pub pred_e_pub_sep trust_politician PPP_pubbdg year
keep if year >= 2012
duplicates drop

* Predicted elasiticity of donations grouped by political trust
twoway (scatter pred_e_pub_sep PPP_pubbdg if year >= 2015 & trust_politician == 1, mcolor(%30))   ///
	   (scatter pred_e_pub_sep PPP_pubbdg if year >= 2015 & trust_politician == 2, mcolor(%30))  ///
	   (scatter pred_e_pub_sep PPP_pubbdg if year >= 2015 & trust_politician == 3, mcolor(%30)),  ///
	   yline(0, lpattern(dash) lcolor(red))  ///
	   xtitle("Social Welfare Budge per capita") ///
	   ytitle("Predicted Elasticity")  ///
	   legend(lab(1 "Lowest trust") lab(2 "Lower trust") lab(3 "Neutral") ///
	   title("Trust for Politicians", size(medium)))  ///
	   title("Predicted Budget-Elasticity of Donations Grouped by Trust")  
graph export "_assets\trust_elast.png", width(700) replace
```

```{r, echo = FALSE, out.width="100%", fig.cap="政治家への信頼と寄付の弾力性"}
knitr::include_graphics(paste(here(), "_assets/trust_elast.png", sep= "/"))
```

## Interaction between Giving Prices and Government Expenditure

最後に、政府の寄付税制政策に基づく寄付価格と政府支出の交互作用を推計する。
推計式は、

$$ \log D_{ijt} = \beta_0 + \beta_1 \log G_{jt}*\log P_{ijt} + \beta_2 (\log G_{jt})^2*\log P_{ijt} + \gamma_1 \log I_{ijt} + \gamma_2 \log P_{ijt} + \delta X_{ijt} + \mu_i + \lambda_t + \epsilon_{ijt}. $$

結果として、**寄付価格と政府支出の交互作用は統計的に有意な影響を持っていなかった**

```{stata price_expenditure, results = "hide", eval = TRUE}
/*******************************************************************************

Interactions b/w giving prices and government expenditure

*******************************************************************************/

* intensive + extensive margin (public services)

* model 1: baseline
eststo: xtreg log_total_g c.log_PPP_pubbdg##c.log_price log_pinc_all i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 2: + age
eststo: xtreg log_total_g c.log_PPP_pubbdg##c.log_price log_pinc_all age i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 3: + education-time dummies
eststo: xtreg log_total_g c.log_PPP_pubbdg##c.log_price log_pinc_all age i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 4: + gender-time dummies
eststo: xtreg log_total_g c.log_PPP_pubbdg##c.log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 5: + living_area
eststo: xtreg log_total_g c.log_PPP_pubbdg##c.log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 6: + squared logged government budge
eststo: xtreg log_total_g c.log_PPP_pubbdg##c.log_price sqlog_PPP_pubpdg c.sqlog_PPP_pubpdg#c.log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

esttab using "_assets\price_expenditure.html", ///
	replace se ///
	keep(log_PPP_pubbdg sqlog_PPP_pubpdg log_price ///
	c.log_PPP_pubbdg#c.log_price c.sqlog_PPP_pubpdg#c.log_price log_pinc_all)  ///
	order(log_PPP_pubbdg sqlog_PPP_pubpdg log_price ///
	c.log_PPP_pubbdg#c.log_price c.sqlog_PPP_pubpdg#c.log_price log_pinc_all) ///
	title("Interactions between Giving Prices and Government Expenditure") 
```

```{r, echo = FALSE}
htmltools::includeHTML(paste(here(), "_assets/price_expenditure.html", sep= "/"))
```