
<<dd_include: report/header.txt>>

# 地方政府予算と寄付行動の関係

地方政府予算のデータをNaSTaBのデータにマージさせて、地方政府予算と寄付行動の関係を調べた。

~~~~
<<dd_do: noout>>
cd "C:\Users\vge00\Desktop\nastab"  //root path

* call data
use "data\shaped.dta", clear

* variables
gen price = .
replace price = 1 - mtr if year < 2014
replace price = 1 - 0.15 if year >= 2014

gen log_price = ln(price)
gen log_total_g = ln(i_total_giving + 1)
gen log_pinc_all = ln(lincome + 100000)
gen log_PPP_pubbdg = ln(pubbdg + 1)
gen sqlog_PPP_pubpdg = log_PPP_pubbdg^2
gen log_PPP_healthbdg = ln(healthbdg + 1)
gen sqlog_PPP_healthpdg = log_PPP_healthbdg^2
gen welfarebdg = pubbdg + healthbdg
gen PPP_welfarebdg = welfarebdg/pop
gen log_PPP_welfarebdg = ln(PPP_welfarebdg + 1)

* panel data
xtset pid
<</dd_do>>
~~~~

## 社会福祉予算の分析結果

### 社会福祉予算と寄付行動の相関

Extensive + Intenstive、Extensive、Intensiveに分けて分析した結果である。
各カラムのラベルは以下の要素をコントロールしていることを意味している

- `twoFE`: Two-way fixed effect model
- `AddAge`: Two-way fixed effect model + 年齢
- `AddEduc`: Two-way fixed effect model + 年齢 + 教育年数・時間ペアのダミー
- `AddGender1`: Two-way fixed effect model + 年齢 + 教育年数・時間ペアのダミー + 性別・時間ペアのダミー
- `AddGender2`: Two-way fixed effect model + 年齢 + 教育年数・時間ペアのダミー + 性別・時間ペアのダミー

全てのモデルに寄付価格の対数値（`log_price`）、所得の対数値（`log_pinc_all`）と一人当たりの社会福祉予算の対数値（`log_PPP_pubbdg`）を加えている。
また、`AddGender2`は社会福祉予算の対数値の二乗項（`sqlog_PPP_pubpdg`）を加えている。
二乗項を入れたとき、弾性値$\epsilon_{jt}$は以下のように計算される。

$$
\hat{\epsilon}_{jt} = \hat{\beta}_1 + 2 \hat{\beta}_2 \text{log_PPP_pubbdg}_{jt}
$$

ここで、$\beta_1$は`log_PPP_pubbdg`の係数で、$\beta_2$は`sqlog_PPP_pubpdg`の係数である。

~~~~
<<dd_do: noout>>
* intensive + extensive margin (public services)

* model 1: baseline
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all i.year ///
	if year >= 2012, fe vce(cluster pid)

estimates store twoFE

* model 2: + age
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddAge

* model 3: + education-time dummies
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddEduc

* model 4: + gender-time dummies
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddGender1

* model 5: + squared logged government budge
xtreg log_total_g log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddGender2

gen pred_e_pub = _b[log_PPP_pubbdg] + 2 * _b[sqlog_PPP_pubpdg] * log_PPP_pubbdg
<</dd_do>>
~~~~

~~~~
<<dd_do: nocom>>
estimates table twoFE AddAge AddEduc AddGender1 AddGender2, ///
	keep(log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all) ///
	b(%9.3f) se(%9.3f) p(%9.3f) stats(N r2) ///
	title("Intensive + Extensive Margin")  ///
	modelwidth(12) varwidth(18)
estimates clear
<</dd_do>>
~~~~

~~~~
<<dd_do: noout>>
* extensive margin (public services)

* model 1: baseline
xtreg i_ext_giving log_PPP_pubbdg log_price log_pinc_all i.year ///
	if year >= 2012, fe vce(cluster pid)

estimates store twoFE

* model 2: + age
xtreg i_ext_giving log_PPP_pubbdg log_price log_pinc_all age i.year ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddAge

* model 3: + education-time dummies
xtreg i_ext_giving log_PPP_pubbdg log_price log_pinc_all age i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddEduc

* model 4: + gender-time dummies
xtreg i_ext_giving log_PPP_pubbdg log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddGender1

* model 5: + squared logged government budge
xtreg i_ext_giving log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

estimates store AddGender2
<</dd_do>>
~~~~

~~~~
<<dd_do: nocom>>
estimates table twoFE AddAge AddEduc AddGender1 AddGender2, ///
	keep(log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all) ///
	b(%9.3f) se(%9.3f) p(%9.3f) stats(N r2) ///
	title("Extensive Margin")  ///
	modelwidth(12) varwidth(18)
estimates clear

<</dd_do>>
~~~~

~~~~
<<dd_do: noout>>
* intensive margin (public services)

* model 1: baseline
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all i.year ///
	if i_ext_giving == 1 & year >= 2012, fe vce(cluster pid)

estimates store twoFE

* model 2: + age
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year ///
	if i_ext_giving == 1 & year >= 2012, fe vce(cluster pid)

estimates store AddAge

* model 3: + education-time dummies
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.educ ///
	if i_ext_giving == 1 & year >= 2012, fe vce(cluster pid)

estimates store AddEduc

* model 4: + gender-time dummies
xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if i_ext_giving == 1 & year >= 2012, fe vce(cluster pid)

estimates store AddGender1

* model 5: + squared logged government budge
xtreg log_total_g log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if i_ext_giving == 1 & year >= 2012, fe vce(cluster pid)

estimates store AddGender2
<</dd_do>>
~~~~

~~~~
<<dd_do: nocom>>
estimates table twoFE AddAge AddEduc AddGender1 AddGender2, ///
	keep(log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all) ///
	b(%9.3f) se(%9.3f) p(%9.3f) stats(N r2) ///
	title("Intensive Margin")  ///
	modelwidth(12) varwidth(18)
estimates clear
<</dd_do>>
~~~~

