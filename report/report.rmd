---
title: "Monetary Donations, Government Expenditure, and Giving Prices (Tentative)"
date: "`r format(Sys.time(), '%Y/%m/%d')`"
output:
  html_document:
    toc: TRUE
    toc_float: TRUE
---

<style>

table, td, th {
  border: none;
  padding-left: 1em;
  padding-right: 1em;
  min-width: 50%;
  margin-left: auto;
  margin-right: auto;
  margin-top: 1em;
  margin-bottom: 1em;
}

</style>



# Crowding-out Effect of Governement Expenditure

地方政府が住民の利益になるような政策を実施することをその地域での公共財投資とみなして、
地方政府の支出の増加による金銭的寄付のクラウディングアウトの程度を分析する。

## データと分析手法

**National Survey of Tax and Benefit (NaSTaB)**：ある年$t$において地域$j$に住む個人$i$

- `log_total_g`：個人$i$の寄付総額の対数値（$\log D_{ijt}$）
- `log_pinc_all`：個人$i$の$t$年の労働所得の対数値（$\log I_{ijt}$）
- `log_price`：個人$i$の$t$年の寄付価格の対数値（$\log P_{ijt}$）
    - 2014年以前は1から年間労働所得で計算した限界所得税率（`mtr`）を引いた値（所得控除制度）
	- 2014年以降は1から共通の税額控除率（15\%）を引いた値（税額控除制度）
- `welfare_level_3scale`：$t$年調査の「今の納税に対して政府の公共政策は十分か」という質問に対する5段階の回答を3段階に集約する

**ソースはどこ？**：ある年$t$における地域$j$の地方政府統計

- `log_PPP_pubbdg`：$t$年における地方政府$j$の社会福祉分野に関する一人当たり支出の対数値（$\log G_{jt}$）
	- 社会福祉分野：基礎生活保障、脆弱階層支援、保育·家族及び女性、老人·青少年、労働、報勲、住宅、社会福祉一般
	- `sqlog_PPP_pubpdg`：この変数の二乗項、$(\log G_{jt})^2$
- `log_PPP_healthbdg`：$t$年における地方政府$j$の保健分野に関する一人当たり支出の対数値（$\log G_{jt}$）
	- 保健分野：保健医療、食品医薬安全
	- `sqlog_PPP_healthpdg`：この変数の二乗項、$(\log G_{jt})^2$
- `log_PPP_pub_j`：$t$年における地域$j$以外の地方政府の社会福祉分野に関する一人当たり平均支出の対数値（$\log G_{-jt}$）
	- `sqlog_PPP_pub_j`：この変数の二乗項、$(\log G_{-jt})^2$
- `log_PPP_health_j`：$t$年における地域$j$以外の地方政府の保健分野に関する一人当たり平均支出の対数値（$\log G_{-jt}$）
	- `sqlog_PPP_health_j`：この変数の二乗項、$(\log G_{-jt})^2$


```{stata dataset, collectcode=TRUE, results = "hide", eval = FALSE}
cd "C:\Users\vge00\Desktop\nastab"  //root path
use "data\shaped.dta", clear

gen price = .
replace price = 1 - mtr if year < 2014
replace price = 1 - 0.15 if year >= 2014

gen log_price = ln(price)
gen log_total_g = ln(i_total_giving + 1)
gen log_pinc_all = ln(lincome + 100000)
gen log_PPP_pubbdg = ln(PPP_pubbdg + 1)
gen sqlog_PPP_pubpdg = log_PPP_pubbdg^2
gen log_PPP_healthbdg = ln(PPP_healthbdg + 1)
gen sqlog_PPP_healthpdg = log_PPP_healthbdg^2

gen welfare_level_3scale = .
replace welfare_level_3scale = 1 if welfare_level < 3
replace welfare_level_3scale = 2 if welfare_level == 3
replace welfare_level_3scale = 3 if welfare_level > 3
replace welfare_level_3scale = . if welfare_level == .

egen group_id = group(living_area)

gen PPP_pub_j = .
forvalues i = 2007(1)2018 {
	forvalues j = 1(1)17{
		qui sum PPP_pubbdg if year == `i' & group_id != `j'
		replace PPP_pub_j = r(mean) if year == `i' & group_id == `j'
	}
}

gen PPP_health_j = .
forvalues i = 2007(1)2018 {
	forvalues j = 1(1)17{
		qui sum PPP_healthbdg if year == `i' & group_id != `j'
		replace PPP_health_j = r(mean) if year == `i' & group_id == `j'
	}
}

gen log_PPP_pub_j = log(PPP_pub_j + 1)
gen log_PPP_health_j = log(PPP_health_j + 1)
gen sqlog_PPP_pub_j = log_PPP_pub_j^2
gen sqlog_PPP_health_j = log_PPP_health_j^2

xtset pid
```

**Two-way fixed effect model**

$$ 
	\log D_{ijt} = \beta_0 + \beta_1 \log G_{jt} + \beta_2 (\log G_{jt})^2 + \gamma_1 \log I_{ijt} + \gamma_2 \log P_{ijt} + \delta X_{ijt} + \mu_i + \lambda_t + \epsilon_{ijt}. 
$$

- $(\mu_i, \lambda_t)$：個人$i$と時点$t$の固定効果
- $\epsilon_{ijt}$：誤差項
- $X_{ijt}$：個人$i$の共変量
	- `age`：$t$年における個人$i$の年齢
	- `i.year##i.educ`：個人$i$の教育年数と$t$年ダミーの交差項
	- `i.year##i.gender`：個人$i$の性別と$t$年ダミーの交差項
	- `i.living_area`：$t$年における個人$i$の居住地域$j$のダミー変数

このモデルから、寄付の政府支出に対する弾力性$\eta_{GD}$は次のように計算される。

$$ \eta_{GD} = \beta_1 + 2 \beta_2 \log G_{jt}. $$

## Baseline Results

ベースラインの結果として、$\log G_{jt}$に社会福祉分野の政府支出を用いた結果を示す。

- モデル(1)～(4)：地域ダミー`i.living_area`を加えていないとき、弾性値（`log_PPP_pubbdg`の係数）は約0.1\%であるが、統計的に非有意である（もしくは10\%水準で統計的に有意）。
- モデル(5)：弾性値は0.839\%であり、統計的に有意である。すなわち、政府支出の増加は市民の寄付を増やす（**クラウドイン**）。
- モデル（６）：弾力性に異質性があることを示している。弾力性の予測値は$$ \hat{\eta}_{DG} = 7.729 - 2 * 0.529 * \log G_{jt} $$で得られる。


```{stata baseline1, results = "hide", eval = FALSE}
* intensive + extensive margin (public services)

* model 1: baseline
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 2: + age
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 3: + education-time dummies
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 4: + gender-time dummies
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 5: + living_area
eststo: xtreg log_total_g log_PPP_pubbdg log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 6: + squared logged government budge
eststo: xtreg log_total_g log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

esttab using "_assets\baseline1.html", ///
	replace se keep(log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all)  ///
  order(log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all) ///
	title("Social Welfare Expenditure and Monetary Donations") 
```

```{r, echo = FALSE}
htmltools::includeHTML("C:/Users/katoo/Desktop/nastab/_assets/baseline1.html")
```

- 弾性値の予測値と一人当たり政府支出の関係を示した図を以下に示した。
	- 政府支出の増加に伴い、寄付の弾力性はマイナス方向に向かう。すなわち、**政府支出の増大に伴い、金銭的寄付はクラウドインからクラウドアウトに向かう**。

```{stata plot baseline1, echo = FALSE, results = "hide", eval = FALSE}
xtreg log_total_g log_PPP_pubbdg sqlog_PPP_pubpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

gen pred_e_pub = _b[log_PPP_pubbdg] + 2 * _b[sqlog_PPP_pubpdg] * log_PPP_pubbdg
keep pred_e_pub PPP_pubbdg year
keep if year >= 2012
duplicates drop

twoway	(scatter pred_e_pub PPP_pubbdg, mcolor(%30)), ///
        yline(0, lpattern(dash) lcolor(red)) ///
        xtitle("Social Welfare Budget per capita")  ///
        ytitle("Predicted Elasticity")  ///
        title("Predicted Budget-Elasticity of Donations")  
graph export "_assets\baseline1.png", width(700) replace
```

```{r, echo = FALSE, out.width="80%", fig.cap="社会福祉予算と弾力性の散布図"}
knitr::include_graphics("C:/Users/katoo/Desktop/nastab/_assets/baseline1.png")
```


## Robustness: 保健分野支出の結果

ロバスト分析として、$\log G_{jt}$に保健分野に関する政府支出を用いた結果を示す。

- モデル(1)～(4)：地域ダミー`i.living_area`を入れていないとき、弾性値は約0.1\%となり、統計的に有意である
- モデル(5)：地域ダミー`i.living_area`を加えたとき、弾性値は0.372\%となり、統計的に有意である
- モデル(6)：弾力性に異質性があることを示している。弾力性の予測値は$$ \hat{\eta}_{DG} = 3.142 - 2 * 0.333 * \log G_{jt} $$で得られる。

```{stata baseline2, results = "hide", eval = FALSE}
* intensive + extensive margin (healthcare services)

* model 1: baseline
eststo: xtreg log_total_g log_PPP_healthbdg log_price log_pinc_all i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 2: + age
eststo: xtreg log_total_g log_PPP_healthbdg log_price log_pinc_all age i.year ///
	if year >= 2012, fe vce(cluster pid)

* model 3: + education-time dummies
eststo: xtreg log_total_g log_PPP_healthbdg log_price log_pinc_all age i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 4: + gender-time dummies
eststo: xtreg log_total_g log_PPP_healthbdg log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 5: + living_area dummies
eststo: xtreg log_total_g log_PPP_healthbdg log_price log_pinc_all age i.living_area i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 6: + squared logged government budge
eststo: xtreg log_total_g log_PPP_healthbdg sqlog_PPP_healthpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

esttab using "_assets\baseline2.html", ///
	replace se keep(log_PPP_healthbdg sqlog_PPP_healthpdg log_price log_pinc_all)  ///
  order(log_PPP_healthbdg sqlog_PPP_healthpdg log_price log_pinc_all) ///
	title("Public Health Expenditure and Monetary Donations") 
```

```{r, echo = FALSE}
htmltools::includeHTML("C:/Users/katoo/Desktop/nastab/_assets/baseline2.html")
```


- 弾性値の予測値と一人当たり政府支出の関係を示した図を以下に示した。
	- 政府支出の増加に伴い、寄付の弾力性はマイナス方向に向かう。すなわち、**政府支出の増大に伴い、金銭的寄付はクラウドインからクラウドアウトに向かう**。
	- ベースラインの結果と比較して、弾性値の異質性は大きくない。社会福祉分野支出を用いるとき、弾性値はおおよそ$[-0.5, 1.5]$の中に入る。その一方で、保健分野支出を用いるとき、弾性値はおおよそ$[-0.4, 0.6]$の中に入る。

```{stata plot baseline2, echo = FALSE, results = "hide", eval = FALSE}
xtreg log_total_g log_PPP_healthbdg sqlog_PPP_healthpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

gen pred_e_health = _b[log_PPP_healthbdg] + 2 * _b[sqlog_PPP_healthpdg] * log_PPP_healthbdg
keep pred_e_health PPP_healthbdg year
keep if year >= 2012
duplicates drop

twoway	(scatter pred_e_health PPP_healthbdg, mcolor(%30)), ///
        yline(0, lpattern(dash) lcolor(red)) ///
        xtitle("Healthcare Budget per capita")  ///
        ytitle("Predicted Elasticity")  ///
        title("Predicted Budget-Elasticity of Donations")  
graph export "_assets\baseline2.png", width(700) replace
```

```{r, echo = FALSE, out.width="80%", fig.cap="保健分野予算と弾力性の散布図"}
knitr::include_graphics("C:/Users/katoo/Desktop/nastab/_assets/baseline2.png")
```


## Robustness: 主観的福祉水準と政府支出の関係

ロバスト分析として、地方政府の一人当たり支出がその地域の公共財のサイズに貢献していると認識しているかどうかを確かめる。

- $t$年の`welfare_level_3scale`でグループ分けをして、政府支出の時系列変化を示した
	- グループ間で政府支出の水準に大きな差がなく、地方政府一人当たり支出が公共財供給のvariationとして使えるのかが怪しい
	
```{stata plot robustness1, results = "hide", echo = FALSE, eval = FALSE}
* plot
bysort year welfare_level_3scale: egen meanpub = mean(PPP_pubbdg)
bysort year welfare_level_3scale: egen meanhealth = mean(PPP_healthbdg)
keep year welfare_level_3scale meanpub meanhealth
keep if welfare_level_3scale != .
duplicates drop

* plot: Social welfare budget
twoway 	(connected meanpub year if welfare_level == 1)  ///
		(connected meanpub year if welfare_level == 2)  ///
		(connected meanpub year if welfare_level == 3),  ///
		legend(label(1 "Small") label(2 "Neutral") label(3 "Large") ///
		subtitle("Perceived Welfare Size") rows(1))  ///
		xtitle("Year") ytitle("Social Welfare Budget per capita") ///
		title("Time-series of Local Governement Budget")

graph export "_assets/robust1.png", width(700) replace

* plot: Healthcare budget
twoway 	(connected meanhealth year if welfare_level == 1)  ///
		(connected meanhealth year if welfare_level == 2)  ///
		(connected meanhealth year if welfare_level == 3),  ///
		legend(label(1 "Small") label(2 "Neutral") label(3 "Large") ///
		subtitle("Perceived Welfare Size") rows(1))  ///
		xtitle("Year") ytitle("Healthcare Budget per capita") ///
		title("Time-series of Local Governement Budget")
	
graph export "_assets/robust2.png", width(700) replace
```

```{r, echo = FALSE, out.width="80%", fig.cap="社会福祉分野支出の時系列プロット"}
knitr::include_graphics("C:/Users/katoo/Desktop/nastab/_assets/robust1.png")
```

```{r, echo = FALSE, out.width="80%", fig.cap="保健分野支出の時系列プロット"}
knitr::include_graphics("C:/Users/katoo/Desktop/nastab/_assets/robust2.png")
```


- Two-way fixed effectモデルを用いて、主観的福祉水準と地方政府支出の相関を確かめた。$$ \text{welfare_level_3scale}_{ijt} = \alpha + \beta \cdot \text{log_PPP_pubbdg}_{jt} + \delta X_{ijt} + \mu_i + \lambda_t + \epsilon_{ijt}.$$
- その結果、二つの変数は負の相関をしていた。
	- この結果からも地方政府の一人当たり支出が公共財供給としてみなされているかどうかが怪しい

```{stata robustness1, results = "hide", eval = FALSE}
* two-way FE
eststo: xtreg welfare_level_3scale log_PPP_pubbdg i.year ///
	if year >= 2012, fe vce(cluster pid)

eststo: xtreg welfare_level_3scale log_PPP_pubbdg i.year i.living_area ///
	if year >= 2012, fe vce(cluster pid)

eststo: xtreg welfare_level_3scale log_PPP_healthbdg i.year ///
	if year >= 2012, fe vce(cluster pid)

eststo: xtreg welfare_level_3scale log_PPP_healthbdg i.year i.living_area ///
	if year >= 2012, fe vce(cluster pid)

esttab 	using "_assets/robustreg1.html",  ///
		replace se ///
		keep(log_PPP_pubbdg log_PPP_healthbdg) ///
		order(log_PPP_pubbdg log_PPP_healthbdg) ///
		title("Corrleration between local governement budget and perceived welfare size")
```

```{r, echo = FALSE}
htmltools::includeHTML("C:/Users/katoo/Desktop/nastab/_assets/robustreg1.html")
```

## Robustness: 居住地域以外の政府支出との相関

ロバスト分析として、回答者の寄付行動が居住地域の慈善団体に対して行われているものかどうかを確かめた。
もしそうならば、回答者の寄付行動と他地域の政府支出に相関はないはずである。
これを確かめるために、以下のモデルを用いて分析した。

$$ 
	\log D_{ijt} = \beta_0 + \beta_1 \log G_{-jt} + \beta_2 (\log G_{-jt})^2 + \gamma_1 \log I_{ijt} + \gamma_2 \log P_{ijt} + \delta X_{ijt} + \mu_i + \lambda_t + \epsilon_{ijt}. 
$$

その結果、**他地域の一人当たり政府支出の平均と寄付額は負の相関をしていた**。
すなわち、他地域の一人当たり政府支出が上昇すると、自身の寄付額を減らす（クラウドアウトと言っていいのか？）。

```{stata robustness2, results = "hide", eval = FALSE}
* model 1: log_total_g ~ log_PPP_pub_j + cov
eststo: xtreg log_total_g log_PPP_pub_j log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 2: + sq_log_PPP_pub_j + cov
eststo: xtreg log_total_g log_PPP_pub_j sqlog_PPP_pub_j sqlog_PPP_healthpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)
	
* model 3: log_total_g ~ log_PPP_health_j + cov
eststo: xtreg log_total_g log_PPP_health_j log_price log_pinc_all age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)

* model 4: + sq_log_PPP_health_j + cov
eststo: xtreg log_total_g log_PPP_health_j sqlog_PPP_health_j sqlog_PPP_healthpdg log_price log_pinc_all ///
	age i.year##i.gender i.year##i.educ ///
	if year >= 2012, fe vce(cluster pid)	

* estimation table
esttab	using "_assets/robustreg2.html", ///
		se replace ///
		keep(log_PPP_pub_j sqlog_PPP_pub_j log_PPP_health_j sqlog_PPP_health_j log_price log_pinc_all) ///
		order(log_PPP_pub_j sqlog_PPP_pub_j log_PPP_health_j sqlog_PPP_health_j log_price log_pinc_all)
```

```{r, echo = FALSE}
htmltools::includeHTML("C:/Users/katoo/Desktop/nastab/_assets/robustreg2.html")
```